

## 第十四章：14 转义



![](img/chapter.jpg)

本章讨论的是一个令人头疼的问题及其批处理解决方案。问题在于，有时你想将某个字符作为普通文本使用，但该字符在编码语言中有特定的功能。解决方案是转义。

在本章中，我将解释如何在批处理语言中转义字符的所有复杂细节。大多数时候会使用某种特定的语法，除了在需要时会有不同。你将了解多轮转义、语法，以及为什么你可能需要多次转义一个字符。我还会回到“续行符”，它用于将命令延续到多行代码，因为当你揭开它的面纱时，你会发现它原来是一个转义字符。然而，在讨论如何解决这个问题之前，你首先需要理解并欣赏这个问题的本质。

### 问题陈述

你可能希望在某些代码中使用特定字符，但如果该字符是编码语言中具有某种预定义功能的特殊字符怎么办？例如，假设你尝试在文本字符串中使用该特殊字符。这种情况在所有语言中都会发生，但在批处理语言中比较常见，因为该语言具有独特的深奥语法。

正如你在本书中反复看到的那样，百分号用于限定变量；百分号位于变量的两边时，变量会被解析为它的值。但是，在批处理语言出现之前，百分号被用来表示百分比——即 100 的比例。因此，批处理中的文本字符串不能简单地表示 50%，否则百分号就会被当作分隔符来解析。这个问题的隐秘之处在于，没有编译器能捕捉到这个问题，解释器甚至可能不会失败，而是产生意外的结果。

举个例子，考虑一下这个命令，它将看似简单的语句写入控制台：

```
> con echo Between 60% and 80% of Americans don't understand percents!
```

解释器将两个百分号之间的内容（即后面跟着 80 的空格）视为一个变量。假设该变量没有设置，这是几乎可以肯定的，那么它（连同百分号）会解析为空值。结果就是在控制台上写出这样一个无意义的语句：

```
Between 60 of Americans don't understand percents
```

如果该命令只使用了一个百分号，它本身就会被从输出中去掉。顺便问一下，结尾处的感叹号去哪了？先记住这个问题。

解决这个难题的方法是转义任何特殊字符。转义字符可能很棘手，但在许多情况下，它们非常有用且不可或缺。很快，我会回到前面的 `echo` 命令，向你展示如何让它输出所需的文本。

### 插入符号转义字符

主要的 Batch 转义字符是插入符号 (^)。在其他语境中，它被称为帽子符号或用来表示指数，但在 Batch 领域，它就是插入符号。在大多数键盘上，你可以通过按 SHIFT-6 来输入它。关键是，解释器会把插入符号后面的大多数字符视为普通文本。

以下 echo 命令试图向控制台输出一些陈词滥调，那些你可能在由不理解 *办公室空间* 是一部喜剧的办公室里的坏激励海报上看到的内容，但令人尴尬的内容只是问题的一部分。它根本无法工作：

```
> con echo Together We Are > You & Me Alone
```

解释器将第二个大于号视为第二个重定向符号，在当前目录中创建一个名为 You 的无扩展名文件，而 & 符号则结束一个命令并开始另一个命令。显然，带有 Alone 参数的 Me 命令会直接失败。

这个命令显然很乱，但通过插入符号可以修复。我在之前被阻碍的两个字符前插入了主要的 Batch 转义字符：

```
> con echo Together We Are ^> You ^& Me Alone
```

你可以将这个命令中的每个插入符号看作一个特殊的信使。每当解释器遇到转义字符时，它就会接收到这个清晰的信号：

紧接着我的下一个字符将被视为普通文本。不要像通常那样解析它。哦，顺便说一下，迅速丢弃我，因为我不过是一个数字版的斐迪比德斯，一个在完成任务后就消失的简单信使。

结果是这个或许具有激励性和鼓舞人心的信息写入了控制台：

```
Together We Are > You & Me Alone
```

另一种让解释器将特殊字符视为文本的方式是将字符串用双引号括起来：

```
> con echo "Together We Are > You & Me Alone"
```

需要注意的是，虽然这个命令没有转义字符，但它也会将双引号输出到控制台。

我很快会揭示一些例外情况，但插入符号是最常用的 Batch 转义字符，你可以用它来转义小于符号 (<)、管道符号 (|) 和圆括号 (())，以及其他特殊字符。但 Batch 并不平等对待所有字符。

### 转义插入符号

由于解释器将插入符号视为转义字符并将其丢弃，你可能会思考一个插入符号希望仅作为文本来处理的困境。例如，如果你要在控制台上写出毕达哥拉斯定理，我希望你不会惊讶地发现 Batch 不支持上标：A² + B² = C²。相反，如果我们能够让它起作用，插入符号表示指数运算会足够：A² + B² = C²。（毕达哥拉斯定理假设 A 和 B 是直角三角形的两条直角边，C 是斜边。）这可能是解决方案的第一次尝试：

```
> con echo The Pythagorean Theorem:  A² + B² = C²
```

不幸的是，每个插入符号都会告诉解释器将随后的字符（每个实例中的 2）视为普通文本，而这本来它就会如此处理：

```
The Pythagorean Theorem:  A2 + B2 = C2
```

解释器只是将插入符号丢弃，好像它们从未存在过。

解决方案在于插入符号是自转义的；一个插入符号用另一个插入符号进行转义。我已将以下代码中的每个插入符号替换为双插入符号。在每个实例中，第一个插入符号是转义字符，后面是文本插入符号：

```
> con echo The Pythagorean Theorem:  A^² + B^² = C^²
```

现在你会得到期望的结果：

```
The Pythagorean Theorem:  A² + B² = C²
```

我们仍然无法管理上标，但写入控制台的结果是最接近的，并且很符合数学家的喜好。

### 转义百分号和感叹号

在学习了勾股定理后，你可能会在数学考试中获得满分，但这个庆祝性批处理命令无法产生预期的结果，因为两个特殊字符从写入控制台的文本中被丢弃了：

```
> con echo I Scored 100% on my Math Test!
```

哎呀！我们忘记了插入符号。你可能会认为这个快速修复会显示百分号和感叹号：

```
> con echo I Scored 100^% on my Math Test^!
```

但输出没有改变。不幸的是，正如批处理中的常见情况那样，存在一些限制。插入符号不能作为百分号或感叹号的转义字符。

百分号的转义字符是另一个百分号，而感叹号的转义字符——实际上是转义字符（复数）——是两个插入符号。如果这对你没有任何意义，你并不孤单。我从未找到过一个合理的解释来说明这个异常，但以下命令会将“I Scored 100% on my Math Test!”写入控制台：

```
> con echo I Scored 100%% on my Math Test^^!
```

思考一下；与勾股定理示例相比，Batch 如何处理双插入符号似乎存在矛盾。文本^² 会解析为²，但与数学测试相关时，文本^^!会解析为!，完全没有留下插入符号。是的，就是这样工作。解释器处理双插入符号的方式取决于它后面跟着感叹号或其他任何字符。可以把它看作是一个限制的限制（或者是一个元限制）。

回到本章开头的题目，这条命令写入了期望的文本：

```
> con echo Between 60%% and 80%% of Americans don't understand percents^^!
```

解释器通过每一对百分号和前置于感叹号前的两个插入符号将适当的文本写入控制台。

> 注意

*正如在第三章中提到的，我写这本书时假设始终启用了延迟扩展，但如果它被禁用，Batch 会将感叹号当作任何其他字符来处理，在 Batch 中没有特殊意义，也不需要转义。*

### 多层转义

之前的示例演示了如何通过单层转义将硬编码文本写入控制台，同样的技巧也成功地设置了一个简单的变量，但有一个陷阱。例如，以下的 set 命令解析了两个转义字符并将“Together We Are > You & Me Alone”存储到变量中：

```
set pureDrivel=Together We Are ^> You ^& Me Alone
```

不幸的是，这个变量的用途非常有限。该变量确实包含了两个特殊字符，但如果你尝试将它写入控制台或文件，或者尝试将它传递给另一个命令，它将无法按预期工作。因为在将该文本赋值给变量时，转义字符被移除了，所以当该变量稍后被解析时，解释器认为具有特殊意义的字符又会引发原本转义解决的问题。

解决方案是转义转义字符——是的，双重转义。以下两行代码将期望的文本写入控制台，我所说的期望文本是指它包含了一个大于号和一个和号，并且没有转义字符：

```
set pureDrivel=Together We Are ^^^> You ^^^& Me Alone
> con echo %pureDrivel% 
```

为了看清发生了什么，让我们专注于`^^^&`。第一个插入符号是第二个插入符号的转义字符，第三个插入符号是和号的转义字符。解析后，set 命令将`^&`作为变量值的一部分进行存储。当 echo 命令解析该变量时，剩下的插入符号——它刚才被当作文本处理——现在是和号的转义字符，最终只有和号被写入控制台。

让我们看一下整个文本字符串。第一个命令将`pureDrivel`设置为`Together We Are ^> You ^& Me Alone`的值；然后第二个命令将文本`Together We Are > You & Me Alone`写入控制台。

多级转义可能会变得更复杂。例如，如果你在将两个变量拼接成一个更大的变量后再将该第二个变量写入文件，你将需要三轮转义。

至于三重转义的机制，考虑一下这个：由于`^^^^^^^&`中有四个转义字符（即七个插入符号），它解析为`^^^&`。第二轮转义将其解析为`^&`，最终在第三轮解析时变成`&`。转义字符的数量是`2*^n* - 1`，其中*n*是转义的次数。我说它很棘手，但它也挺酷的。

### 连续字符

我不止一次听到程序员将插入符号称为批处理的*连续字符*，我甚至在第五章中介绍过这种用法，举了一个例子，展示了它在一个跨越四行代码的 set 命令中的应用。严格来说，这不正确，但在实践中，它确实执行了这个功能。让我来解释一下。

每个程序员的目标都应该是写出不需要读者左右滚动的代码。（当然，它还应该是高效的、文档完善的、结构清晰的，甚至是优雅的，但这也许只是我的个人看法，所以我离题了。）在大多数编译语言中，当命令过长不容易阅读时，你只需按 ENTER 键，然后在下一行继续输入。编译器足够聪明，知道该命令包含了两行、三行，甚至更多行。

Batch 解释器没有那么宽容（或聪明），但当你在代码行的*最后*添加一个插入符号时，语句会继续到下一行。即使一行代码不特别长，我有时也会使用这种技术将传递给可执行文件的参数对齐，以提高可读性：

```
C:\Batch\SomeExecutable.exe %Arg1% ^
                            %Arg2% ^
                            %AnotherArg% ^
                            %YetAnotherArg% ^
                            %AndAnother% 
```

在文本文件的大多数行末尾，两个字节表示*回车换行符（carriage return line feed）*。在十六进制中，这两个字节是 x'0D' x'0A'，它们通常合在一起称为 CRLF，但在文本编辑器中通常不可见。（如果使用 Notepad++，可以选择**查看** ▶ **显示符号** ▶ **显示行尾**，以便让 CRLF 可见。其他编辑器也有类似功能。）

实际上，插入符号仍然只是一个转义字符，它是在转义 CRLF。遵循转义字符的作用，当解释器看到插入符号时，它不会像通常那样将后面的 CRLF 视为行末，而是将其视为其他空白字符并忽略它，实际上是“换行”了。从这个角度来看，插入符号就是“续行符”。（但我仍然感到不太舒服。）

一个常见的错误是，在行尾看似结束的插入符号后加上一两个空格，从而使文本包装失效。由于插入符号转义的是紧随其后的字符，这样做不过是转义了一个空格，这几乎等于什么都没做，并且不会打乱 CRLF。对于那些仅将插入符号视为续行符而不是转义 CRLF 的用户来说，这种疏忽可能很难排查。知识就是力量。

你已经学到，单个字符通过一个转义字符来转义，除了感叹号，它需要两个转义字符。CRLF 是另一个例外，但原因正好相反。CRLF 实际上是两个字符，回车字符和换行字符，它是 Batch 中唯一一个由一个字符转义的两个字符示例。

### 总结

在本章中，你学习了许多使用插入符号（caret）以及有时使用百分号（percent sign）来转义特殊字符的方法，但讨论才刚刚开始。这项技术是一个不可或缺的工具，稍后在本书中你将看到它的多个应用。

如果没有别的，我希望本章能让你认识到转义是多么棘手。当我还是一个初学者时，我从一位更有经验的同事那里得到了一个简单却富有智慧的建议：要认真测试；测试代码可能遇到的所有字符。由于有许多警告、特殊情况和例外，你不应该在看到转义在某个特定场景下有效后，就假设它在所有环境下都会有效。在你的测试计划中，加入所有可能遇到的特殊字符，来测试那些进行转义的代码。

为了完全不同的内容，下一个章节将讨论如何使批处理文件与人类进行交互，提问、获取回答，并根据这些回答执行条件逻辑。
