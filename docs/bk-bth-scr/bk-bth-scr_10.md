<hgroup>

## 9 标签与非顺序执行

</hgroup>

![](img/chapter.jpg)

在政治领域，标签常常被误解，但在最基本的层面上，*标签*是一个标识符，它以尽可能少的字词简明地定义一个产品或对象。如果没有标签，商业就会停滞不前；超市会堆满满满的神秘罐头产品。晚餐吃什么？可能是豆子，也可能是南瓜派混合粉；我们打开之前无法知道。

没有标签，Batch 文件不会完全陷入这种混乱，但作为创建更复杂批处理文件的重要工具，它将缺失在你的编程工具箱中。到目前为止，本书中的每个批处理文件、代码片段和列表都是按顺序执行的。解释器逐行解释，每次先执行第一条命令，然后是第二条。这一过程会一直继续，直到发生以下两种情况之一：要么是批处理文件的最后一条命令被解释，要么是语法错误导致批处理文件崩溃。标签允许你以非顺序的方式执行 Batch 命令。在本章中，我将介绍如何在代码中进行前后分支、根据数据条件重复某些代码段，甚至创建一些 Batch 本身不包含的命令。

标签还为我提供了一个很好的机会，来讨论一个至关重要的话题：编码规范，尤其是缩进。

### 标签

在 Batch 中，*标签*就是你可能预期的那样，一个定义代码块的标签。更具体地说，批处理文件中的某个位置或点被*标记*为标签。标签本身不是命令，虽然它从未被执行，但你很快会发现它对于执行流程至关重要。

标签可以包含字母、数字和一些特殊字符，最重要的是，它们必须以冒号开头。奇怪的是，标签名可以包含额外的冒号，但绝不能出现在第二个位置。例如，这里有一些代码，按照它的作用进行了定义或标记，用于检查特定变量的状态：

```
:CheckStatus
 if /i "%status%" equ "fail"  > con echo Failure
 if /i "%status%" equ "good"  > con echo Success 
```

类似地，这段代码处理一个非常基本的中止过程，并被标记为这样：

```
:Abort
 echo The Process is aborting
 exit /B 1 
```

我将在第十章中讨论退出命令。现在，它仅用于退出批处理文件。

定义标签非常简单，但在深入了解标签的影响及如何使用它们之前，请允许我稍微偏离一下，甚至有点愤怒地谈谈编码规范。

### 缩进

许多批处理编码员拒绝给他们的代码添加缩进。我不太清楚原因，因为我所熟悉的每种编程语言都有某种约定，即使不是强制要求，也有对缩进的规范。我的最佳猜测是，批处理语言本质上缺乏对其本身的尊重，认为批处理只是一个必须尽快完成的工具性麻烦，根本不考虑可读性，更别提美学了。为了让你的批处理代码赢得应有的尊重，我建议所有命令前加上两个空格的缩进。在 if 命令的代码块（以及其他类似结构，后面会讨论）内部，缩进再加三个空格，嵌套结构则应缩进更多。

这个话题可能在讲解标签的章节中看起来有些不合时宜，但实际上，这是最理想的位置。标签应该稍微突出一点，甚至更突出。任何格式良好的文档都有部分、章节、节和/或小节，每个部分通常都有某种标题或提示——或者我敢说是 *标签*——通过不同的字体、字号、加粗、下划线、着色或上述几种方式的组合，使其在视觉上与其余文本区别开来。然而，在编写 bat 文件时，这些选项都不可用。我们的工具库只剩下一个重要的项目——缩进，同时也可以借助大写字母和空格。

由于标签的第一个字符总是冒号，因此我通常将冒号放在行的第二个字符位置，这样我的典型缩进就减少为一个字符。因此，当任何人，包括我自己，查看我编写的 bat 文件时，所有标签都会非常明显。我将行的第一个字符保留给 rem 命令的开始。例如，下面是一个基本的备注、标签和两个简单的命令：

```
rem - This code does something.
 :DoSomething
  set do=something
  set doMore=somethingElse 
```

标签中的冒号后的大写字母也增加了其显著性。

希望我不是把自己当作批处理编码规范的斯大林。这只是一个编码员的个人意见，实际上还有许多经过深思熟虑、与我不同的规范。重要的是代码应该易于阅读。实现这一点有很多方法，但完全不缩进肯定无法通过测试，即使这个话题让我显得有些专制。

### goto 命令

既然我们已经有了一个定义代码片段的标签，那它有什么用呢？一些编码员实际上将标签当作临时备注（我想这也没问题），但标签的真正功能是指引程序流向标签下方的代码。这就是 goto 命令的作用，它按字面意思指示解释器跳转到由标签定义的代码位置。考虑以下两个命令：

```
goto :Abort
goto :DoSomething 
```

goto 命令将控制转到本章前面定义的 :Abort 和 :DoSomething 标签。

好吧，这不完全正确；第一条命令将执行跳转到 abort 例程，第二个 goto 命令永远不会执行。在 bat 文件中，标签本身可以出现在 goto 命令之前或之后，但重要的是要明白，一旦执行了 goto 命令，执行不会返回到 goto 命令后的那条命令。执行一旦跳转，我们就完全受制于标签下的代码。

要跳转到定义为:Abort 的标签，你也可以使用以下命令：

```
goto ABORT
```

这里有两件事。首先，goto 命令中标签名称的冒号被省略了。我怀疑这是一个早期的错误，微软为了保持向后兼容性可能不会修复。其次，实际的标签只有 A 是大写的，但 goto 命令显示的是整个标签名称都大写。

这个例子演示了标签名称是大小写不敏感的，就像 Batch 通常一样，并且在 goto 命令中冒号是可选的。虽然解释器允许这样做，但我认为没有理由让两个标签名称有所不同，因为这只会引起混乱。保持一致性是关键。

在第八章中介绍的 call 命令也与标签一起使用，但其行为与 goto 命令非常不同。我将在第十章中回到 call 命令和这些区别。

#### 前进分支

goto 命令将控制权或流程转向两个方向之一；一个是跳过代码的前进分支。在这个例子中，三个 echo 命令将文本输出到控制台，但只有第一个和第三个会被执行：

```
 > con echo Before GOTO
 goto :MyLabel
 > con echo After GOTO
:MyLabel
 > con echo After LABEL 
```

很容易想象使用这种技术的更复杂代码。一个 goto 命令可能基于 if 命令的结果有条件地执行，而不仅仅是跳过单个 echo 命令，它可能跳过更大的一段代码。例如，如果某个文件存在或不存在，或者检测到失败，你可以跳转到将中止 bat 文件执行的代码，跳过其他所有内容。

goto 也可以作为跳出循环的工具。不幸的是，我还没有讨论循环；在第二部分中，我将详细讨论 for 命令和循环。但现在，为了理解这个逻辑，你只需要知道这个循环会根据 listOfNames 变量中列出的每个名称执行一次，不管它包含多少个名称：

```
 for %%n in (%listOfNames%) do (
    if /i "%%n" equ "Waldo"  goto :FoundName
 )
 > con echo ** Name Not Found **
:FoundName 
```

if 命令在搜索特定名称。如果找到了，它会让 goto 命令跳出循环，跳转到最后一行的标签。

这很重要，原因有两个。首先，它是高效的——如果名字出现在列表的前面，CPU 不会浪费周期去无谓地搜索列表的其余部分。更重要的是，如果名字被找到，echo 命令就不会被执行。请注意，逻辑不仅提前跳出了循环，而且还跳过了写出未找到名字的消息的部分。

#### 向后跳转

上一节中的示例使用了 goto 命令来跳过代码的前进。接下来，我将展示一些 goto 命令反向跳转的示例。但首先，我已经讨论了如何构建一些更现代语言的组件，这些组件并不是批处理语言的一部分（比如布尔值和浮点数），但很多其他组件还未出现。批处理没有 while 命令，也不支持 do...while 命令。在其他语言中，while 命令会根据条件执行一段代码零次或多次，直到条件满足。do...while 命令也非常类似；唯一的区别是，在评估条件之前，代码块会执行一次。让我们在批处理中创建这两种命令。

##### while “命令”

为了演示批处理 while 命令的有用性，我们将编写一些代码，去除值的所有前导零，这对于任何不想意外执行八进制算术的程序员来说是必要的（如果你错过了，可以在第六章找到详细解释）。while 命令会执行一段代码，只要——或者 *当*——第一个字节是 0，并且这段代码将仅仅做一件事：去除一个前导字节。

以下代码完美地执行了任务：

```
:StripLead0s
 if "%nbr:~0,1%" equ "0" (
    if "%nbr:~1,2%" neq "" (
       set nbr=%nbr:~1%
       goto :StripLead0s
 )  ) 
```

解释器在第一次遇到标签时，基本上会忽略它，并检查 nbr 的第一个字符。如果是 0，接下来代码会验证是否有第二个字节——也就是说，0 是否确实位于某个值的前面。如果两者都成立，代码块会被执行，它会移除前导 0，然后 goto 命令将控制权传回到 if 命令之前的标签。

让我们通过三个不同的数字逐步查看代码，真正理解其逻辑。如果变量没有前导 0，则代码块永远不会执行。如果有一个前导 0，则代码块执行一次。然后，再次检查前导字节，由于它不再是 0，执行流程将继续执行后续的内容。如果 nbr 有 17 个前导 0，移除 0 的代码块会执行 17 次，在前导字节第 18 次检查后，执行流程继续向下进行。

在这个列表中并没有出现 *while* 这个词，但它做的事情和一个标准的 while 命令一样。就我而言，它就是一个批处理中的 while 命令。

> 注意

*前面的代码片段是我第一次展示一个 if 命令嵌套在另一个命令中的例子，但在接下来的章节中，你将看到更多的嵌套命令。关于编码规范的另一个说明，我在那个列表中将两个尾随的右括号堆叠在同一行。这使得代码更紧凑，尤其是在嵌套多个层级时，也使得关注点集中在有趣的逻辑上。不过我承认，我可能是少数派。大多数 Batch 程序员会将每个右括号与其各自的 if 命令对齐。这需要更多的代码行，但以下代码在功能上等同于我之前的代码：*

```
:StripLead0s
 if "%nbr:~0,1%" equ "0" (
 if "%nbr:~1,2%" neq "" (
 set nbr=%nbr:~1%
 goto :StripLead0s
 )
 ) 
```

> *做自己觉得对的事情，并坚持去做。另外，请注意标签名称包含了一个数字值。如前所述，我们并不局限于字母。顺便说一下，那个缩进看起来不错吧？*

##### do...while “命令”

Batch 的 do...while 命令看起来与 while 命令非常相似；唯一的区别在于主逻辑必须至少执行一次。在具有内建 do...while 命令的语言中，条件子句通常位于结构的尾部（可以理解为在主逻辑执行一次后），Batch 也不例外。与 while 命令相比，主逻辑从 if 命令代码块内部移到了标签之后、if 命令之前的位置。

为了演示，让我们以一个例子为例，其中 textStr 变量需要右填充至少一个空格，以便将其扩展到最少 25 字节的长度。如果原始字符串的长度小于 25 字节，结果将是 25 字节；如果它本身已经至少 25 字节长，则会在结果中附加一个空格。（这个字符串可能是某些连接文本的一部分，用于在控制台上显示，其中空格填充将对齐列。当然，我们希望它和接下来的内容之间有一个空格，即使这需要额外的一个字节。）

右填充必须至少执行一次，这使得 do...while 命令非常适用：

```
:PadRight
 set textStr=%textStr% &
 if "%textStr:~24,1%" equ ""  goto :PadRight 
```

与 while 命令一样，标签位于大部分代码之前，但核心逻辑紧随其后，在这种情况下是一个单独的 set 命令，用一个空格填充字符串。然后检查第 25 个字节。（记住，它是零偏移。）如果不存在，goto 命令会将执行返回到标签处，以便可以向字符串中添加另一个空格。这个过程会一直重复，直到第 25 个字节被填充，确保字符串至少为 25 字节长，并且无论长度如何，都至少添加了一个空格。

在第二十六章中，我将详细介绍如何执行自动重启失败的进程，通常只需使用一个标签和 goto 命令—也就是 do...while 命令—就能使进程成功重启。

### :eof 标签

一个不是由程序员创建但对所有 bat 文件内在存在的特殊标签是 :eof，它代表 *文件结束*。当以下的 goto :eof 命令在被调用的 bat 文件的主逻辑中执行时，控制权会返回到调用的 bat 文件：

```
> con echo We are about to exit the bat file.
goto :eof
> con echo This command will never be executed. 
```

在高级 bat 文件中执行相同的命令会完全停止过程，即使 bat 文件中没有定义 :eof 标签。

如果你本性反其道而决定定义自己的 :eof 标签，解释器将简单地忽略它，仿佛它是一个无意义的备注。在第十章中，我将进一步探讨这个独特的标签，特别是解释器如何在可调用例程内部处理 goto :eof 命令。

### 变量标签

在没有编译器的语言中工作确实有一些缺点，但我已经向你展示了一些“银 lining”（例如延迟扩展）。另一个优点是能够在执行时在 goto 命令中定义标签名，尽管标签本身必须是硬编码的。为了设置这个，设想为每个月定义一个不同的标签。这里显示了前三个标签，但它们下方没有与月份相关的代码：

```
:MonthJanuary
:MonthFebruary
:MonthMarch 
```

显然，以下命令将把执行指向前面代码片段中的某个特定标签：

```
goto :MonthFebruary
```

但现在这已经是陈旧的消息了。更有趣的是，如果变量 month 被设置为二月，以下命令将调用相同的标签：

```
goto :Month%month%
```

这个 goto 命令的参数是硬编码的 :Month 和 month 变量的值的拼接。在变量被解析后，命令将执行转向标签 :MonthFebruary。同样的道理也适用于其他有效的月份，这意味着如果 month 被设置为三月，这行代码也会跳转到 :MonthMarch。

但这确实引出了一个问题，那就是当生成的标签名在 bat 文件中不存在时会发生什么，比如，如果 month 被设置为 Erele（在约鲁巴语中表示二月）。解释器会向控制台写入以下消息：

```
The system cannot find the batch label specified - MonthErele
```

不幸的是，你永远不会看到此消息，因为过程会立即崩溃。

在第十章中，你将看到当与 call 命令一起使用时，Batch 可以更好地处理错误的标签名，但如果你使用这种技术与 goto 命令，确保该参数解析为有效的标签。

### 总结

在本章中，我介绍了标签的概念以及如何通过 goto 命令导航到它们。你学习了如何创建标签，探索了它们的使用技巧，并了解了它们在构建 while 和 do...while 命令中的重要作用。我还介绍了不可或缺的 :eof 标签。

但是你可以通过两种不同的方式导航到标签。下一章的大部分内容也将集中在标签上，探讨如何使用它们在 bat 文件内部创建可调用的例程。我还将详细说明如何从一个 bat 文件调用另一个 bat 文件，这是在你开始创建过于复杂的项目时一个至关重要的话题。
