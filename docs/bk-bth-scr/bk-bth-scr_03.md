<hgroup>

## 2 变量和值

</hgroup>

![](img/chapter.jpg)

现在我们准备开始编码，我们将探讨变量、值和 Batch 的 set 命令，后者用于将值分配给变量。虽然这些话题如果你在其他语言中有过编码经验可能显得微不足道，但 Batch 有一些独特的怪癖，值得注意。

你将学习如何在控制台显示变量的值，以确认它已经正确设置。此外，我将介绍 *命令分隔符*，它允许你在一行中输入多个命令。我还会向你展示如何创建注释以及如何设置保存到你计算机上的变量，这些变量即使在关闭 bat 文件后也能继续使用。最后，你将学习如何在命令提示符下访问任何 Batch 命令的文档，这对任何使用 bat 文件的人来说都是一项有用的技能。

### 设置和解析变量

*变量* 是一个命名字段，定义了一个内存位置，用于存储稍后使用的值。许多甚至大多数语言允许并通常要求在赋值之前定义变量的特定数据类型，通常是某种文本或数字类型。但 Batch 不这么做；变量在第一次被“设置”值时就会存在，而且该值可以包含字母、数字和其他字符。之后，程序员可以选择将其视为某种数据类型，或者不做任何处理。在 第五章 和 第六章 中，我会回到如何处理数据类型的方法，但本章重点讨论将值赋给变量这一看似简单的任务。

以变量为例，程序员可能有一个字段来表示他们的心情状态；无论是出于自恋还是强迫症原因都无关紧要。该变量定义为或命名为 myMood，可能的两种值是 happy 和 sad。要将变量设置为 happy，显然可以使用 set 命令：

```
set myMood=happy
```

执行此操作后，myMood 变量将包含值 happy。

如果这个话题特别令人困惑，以下命令会清除之前的值，并用不同的值替换它：

```
set myMood=nonplussed
```

但你怎么能确定这个或任何其他变量的值呢？在 Batch 中，揭示变量的值被称为 *解析变量*，通常是通过将变量用百分号包围来完成的。也就是说，%myMood% 会在执行先前的 set 命令后解析为 nonplussed。现在，为了实际看到解析变量的结果，我们需要稍微偏离一下——一个非常重要的偏离。

### 显示变量的值

在本节中，你将学习如何快速显示计算机屏幕上已解析的变量值，但这种技巧不仅仅用于此。我们将在未来的章节中回到这一点，展示 Batch 的其他许多特性，例如将其作为测试技术，这对于任何 Batch 程序员来说都是至关重要的。

#### 写入控制台

要在屏幕上显示一个变量的内容，我们需要两个额外的命令：echo 和 pause。为了演示，让我们创建一个小的 bat 文件。打开计算机上的一个新文件夹，可能是 *C:\Batch\*，然后在其中创建一个名为 *Mood.bat* 的 bat 文件，文件内容为 Listing 2-1 中显示的三行。

```
set myMood=happy
echo My mood is %myMood%.
pause 
```

Listing 2-1: bat 文件 Mood.bat 显示已解析的变量

如果你双击或打开 *Mood.bat*，bat 文件应该会执行，并且一个黑色窗口将显示白色文字。这个窗口是 DOS 窗口，或称为 *控制台*，在本书中我会称它为控制台。

我们已经在 Listing 2-1 的第一行讨论过 set 命令。在此上下文中，echo 命令将剩余的语句（不包括实际文本 echo 后面的空格）输出到控制台。显示的文本是 "My mood is" 和一个尾随的空格，然后是 myMood 的内容或值——也就是文本 happy——以及尾随的句点。pause 命令保持控制台打开。如果没有它，窗口会在你来得及阅读之前打开并关闭。

Listing 2-2 显示了当这个 bat 文件执行时写入控制台的所有内容，这比你预期的可能还要多。

```
C:\Batch>set myMood=happy

C:\Batch>echo My mood is happy.
My mood is happy.

C:\Batch>pause
Press any key to continue . . . 
```

Listing 2-2: Mood.bat bat 文件的控制台显示

每个命令前面都有当前目录 *C:\Batch\*，然后是一个大于号（>）作为分隔符。（你将在 第八章 中了解当前目录的更多内容。现在，只需将其视为正在执行的 bat 文件的路径。）

第一行显示了 set 命令的执行，第二行显示了 echo 命令的执行。第三行是执行 echo 命令后的结果——即 Listing 2-2 中描述的输出到控制台。你可以从缺少前缀文本 *C:\Batch>* 来判断它不是一个命令。更重要的是，%myMood% 解析为文本 happy。pause 命令也会生成输出，显示文本 Press any key to continue ... ，正如消息所示，执行会暂停，直到按下任意键，此时 bat 文件将结束，控制台也会关闭。另外，注意 set 命令不会生成任何输出，因为它只是设置一个变量的值——没有东西需要输出。

#### 清理控制台

输出到控制台的问题之一是，命令与来自 echo 和 pause 命令的期望输出交织在一起，造成了一团乱。有关不同输出和如何管理它们的更多内容，你将在 第十二章 中学习；在这里，我们只快速看一下清理这些内容的方式。在 *Mood.bat* 文件的顶部执行一个 echo 命令，后跟参数 off，正如 Listing 2-1 中所示：

```
@echo off
set myMood=happy
echo My mood is %myMood%.
pause 
```

echo off 命令并不是抑制后续命令的实际输出，而是抑制显示每个命令正在执行的行——即，显示当前目录的那一行。此外，在 echo 命令前加上 @ 符号，可以抑制它自身执行的内容不显示在控制台上。

与 Listing 2-2 相比，当执行修改后的 bat 文件时，控制台的显示变得更加简洁：

```
My mood is happy.
Press any key to continue . . . 
```

现在我们可以轻松演示如何将 myMood 变量初始化为忧郁值，然后将其重置为快乐值：

```
@echo off
set myMood=gloomy
echo My mood is %myMood%.
set myMood=cheerful
echo Now my mood is %myMood%.
pause 
```

结果显示相同的变量在不同时间解析为两个不同的值：

```
My mood is gloomy.
Now my mood is cheerful.
Press any key to continue . . . 
```

随着我们的 bat 文件变得越来越复杂，echo 命令可以将输出发送到控制台以外的其他地方（有关更多细节，请参见 第十二章）。通过在命令前加上 > con，可以显式地将数据重定向到控制台：

```
> con echo This will always get to the "con" or console
```

这种技术将在后续章节中非常有用，用于演示代码片段中的操作。为了简洁起见，后面的示例中我不会包含初始的 echo off 和尾随的 pause 命令，但我建议你添加它们来清理并保持控制台开启。

### set 命令的特性

设置变量通常是大多数编程语言中的简单话题，但批处理并不像大多数编程语言。所有批处理程序员都需要理解以下 set 命令的特性，以避免将来可能遇到的一些问题。

#### 大小写敏感性

仔细检查以下两个命令。它们看起来有些不同，但功能上是等效的：

```
SET myMood=whimsical
set MYMOOD=whimsical 
```

批处理命令和变量不区分大小写。这里 set 命令在一个命令中是大写的，在另一个命令中是小写的，但解释器将它们视为相同。你也可以使用 Set，而功能不受影响。为了保险起见，sET 和 SeT 也能以相同方式工作，但你得真是个反叛者才会这样编码。同样，你可以交替使用 myMood、MYMOOD 和 mymood 变量。然而，变量的值是按输入的方式存储的，因此它是区分大小写的。如果变量被设置为 WHIMSICAL，它将被解析为 WHIMSICAL；同样，如果设置为 Whimsical，它将被解析为 Whimsical。

这完全是风格和个人偏好的问题。我发现许多 bat 文件中过多的内容都被大写了。大写本应让某些东西突出，但如果一切都在闪烁霓虹灯，什么也就没法突出。大多数批处理程序员将命令名称中的所有字母都大写，但在本书中，我使用小写字符来表示所有批处理命令。此外，我更喜欢使用驼峰命名法的变量。

> 注意

驼峰式命名*文本易于阅读，即使它包含多个没有空格或其他字符分隔的单词。驼峰式命名的第一个字母可以是大写（首字母大写）或小写（首字母小写）。但要符合驼峰式命名规则，所有后续单词的第一个字母必须大写，其余部分小写。一个*首字母大写*变体（也叫做*Pascal*或*大驼峰命名法*）是 MyMood。对应的*首字母小写*变体（也叫做*dromedary*或*小驼峰命名法*）将是 myMood。想象一下，一只骆驼低着头喝水。*

#### 有效的变量字符

大多数编程语言都有严格的规则，规定了变量名中可使用的字符列表。通常，数字和 26 个字母（大小写均可）是允许的，另外还有一些特殊字符可以使用。但批处理语言不同，几乎键盘上的每个字符都是有效的变量名字符，尽管你应该避免将数字作为变量名的第一个字符。（虽然你可以设置以数字开头的变量名，但解决这些变量时会有问题，为什么会这样，我会在第三章中解释。）

一些字符是非法的，因为它们在批处理语言中有特定用途；例如，波浪号 (~)、和号 (&)、百分号 (%)，以及小于号 (<) 和大于号 (>) 是保留字符，但其他一些字符会让任何不熟悉批处理语言的程序员感到惊讶。 列表 2-3 中的三个 set 命令成功地将这三个具有奇怪单字符名称的变量设置为它们各自的描述。

```
set ;=semicolon
set @=at
set #=hashtag

> con echo %;% %@% %#% 
```

列表 2-3：设置具有奇怪单字符名称的变量

列表 2-3 中的 echo 命令将文本分号和井号写入控制台。

即使是以下这种怪异的做法，也能将文本“这确实有效”存储到包含美元符号、点和不匹配括号的变量中：

```
set var$with.oddchars}=This actually works
```

这个变量名展示了可能的内容，但它难以阅读，并不推荐使用。

然而，明智地使用这些字符在变量名中，可以成为一个方便的工具。例如，一组相关的变量可能都带有前导或尾部的下划线，作为它们之间关系的视觉提示；数字可以用 # 来表示，这比 nbr 更简洁，也比 no 更清晰。在本书的后面，我将利用这个有趣的特性构建包含括号的有意义的数组和哈希表。

#### 等号两侧的空格

以下是大多数新手批处理程序员在学习其他语言时常犯的经典错误。仔细检查[列表 2-4 中显示的 set 命令。

```
set X = Hello
> con echo The value of X is "%X%". 
```

列表 2-4：设置带有等号两侧空格的变量

如果你期望 echo 命令的结果是

```
The value of X is "Hello".
```

那将是一个可以理解的错误，但仍然是错误。实际结果是这样的：

```
The value of X is "".
```

空的引号意味着 X 没有被设置，或者被设置为 null，意味着根本没有任何内容，甚至没有空格。

> 注意

*在 第一章 中，我提到过在命令提示符下，语法和输出可能与 bat 文件有所不同，这是一个典型的例子。在命令提示符下输入相同的代码，显示的是对未设置变量的尝试解析，表现得非常不同：*

```
The value of X is "%X%". 
```

> *我不会在接下来的页面中指出每个差异，因此如果你在命令提示符下看到任何异常，尝试将代码放入 bat 文件中。*

现在，Listing 2-4 中的 set 命令并不复杂，显然是在将 X 设置为文本 Hello，对吧？而且它看起来与其他更现代的语言中的赋值命令很像，应该会按预期进行赋值。

这是我们的第一个 *batveat*（批处理警告；有关 batveat 的详细信息，请参见介绍部分）。这个问题的关键是等号前的空格。批处理解释器就像难搞的青少年一样，聪明而且无情，对自己都不好。变量名从 set 命令后的第一个非空格字符开始，到赋值运算符或等号之前的字符结束——不管这个字符是什么。因此，这里设置的变量由两个字符组成，一个 X 后跟一个空格：

```
set X = Hello
```

%X% 没有解析出任何内容，但 %X % 确实有一个值，这个值是 Hello，对吧？其实不完全是；这是我们的第二个警告。变量的值是等号后的字符串，一直到语句的末尾。因此，赋值的内容是等号后面的空格，然后是单词 Hello 中的五个字符。

让我们像这样修改 Listing 2-4 中的 echo 命令：

```
set X = Hello
> con echo The value of X-space is "%X %". 
```

现在，解决带空格的变量会显示其值，而该值包含一个前导空格：

```
The value of X-space is " Hello".
```

这个方法可行，但通常变量名后带空格是一个潜在的意外。我们甚至可以在变量名中间嵌入空格，但能做某件事和做这件事是个好主意之间有很大区别。这绝不应被解读为鼓励创建晦涩的代码；这更像是一个关于在 set 命令中等号两侧空格的警告。

再次查看 Listing 2-4，我们去掉等号前后空格，回到原始的 echo 命令：

```
set X=Hello
> con echo The value of X is "%X%". 
```

最后，我们得到了写入控制台的预期结果：

```
The value of X is "Hello".
```

一个更容易犯的错误是无意中在行末加上一个或两个空格。因为在编辑器中看不到空格，这很容易被忽略。（在 Notepad++ 中，进入 **查看** ▶ **显示符号** ▶ **显示空格和制表符**，可以将空格显示为淡点。其他优秀的编辑器也会有类似的功能。）

在本书后面你会学到，有些有效的原因会将变量的值前置或后置空格，但一定要小心不要不小心这样做。然而，我很难想到一个合法的例子，说明变量的名称后面会被附加一个或多个空格。在使用基本的 set 命令时，确保变量名和等号之间没有空格。

### 命令分隔符

& 符号是一个特殊字符，作为命令分隔符使用，它不像普通文本那样处理。例如，你可以将 Listing 2-3 中的三行代码串联成一行，每个命令之间用 & 字符分隔：

```
set ;=semicolon& set @=at& set #=hashtag
```

这在功能上等同于将三个命令写在三行上。

有时，这种技巧对于合并简单且相似的命令很有用，但过度使用它会使代码难以阅读。然而，我发现了命令分隔符的两个非常有用的应用。

#### 向命令添加备注

命令分隔符的一个用途是将文本添加到一行的末尾，使其被视为注释。rem 命令将其后面的文本变成*注释*。通常，我们会在某些有趣的代码前面（或前几行）放置 rem 命令作为注释，但我们也可以将它与特定命令一起使用，通过命令分隔符来附加它。例如，下面的两行执行相同的逻辑：

```
set myMood=reflective
set myMood=reflective& rem This is a thoughtful and contemplative mood. 
```

然而，第二行为任何阅读代码的人提供了更多的信息。

#### 终止命令

命令分隔符的第二个用途是明确地终止命令，以便编码人员能够清楚地区分是否有多余的空格。以下命令是在将变量设置为空值、一个空格还是多个空格？

```
set myMood=
```

按照现在的写法，不可能告诉（除非你让编辑器显示空格作为可见字符）。

如果你没有阅读前面的讨论，下面的命令可能会让你觉得它正在将变量设置为一个 & 符号：

```
set myMood=&
```

但这个命令明确地告诉读者，变量被设置为空值，因为该语句在等号操作符后立即被 & 符号终止。

同样，编码人员可能希望变量具有特定长度（你将在 第二十二章 中看到这个应用，特别是在格式化报告时）。下面的命令将变量设置为一个 10 字节左对齐的值，包含文本 pensive 后跟三个空格：

```
set myMood=pensive    &
```

如果没有 & 符号，确定文本后面跟了多少个空格（如果有的话）将非常困难。从技术上讲，& 符号并没有分隔两个命令，但它无疑终止了一个命令。

### 显示变量信息

set 命令还有一个有趣的用法。当没有等号时，它会输出变量的值，因此，如果 myMood 变量已经定义，你可以在 bat 文件中输入此命令：

```
set myMood
```

输出到控制台的结果可能如下所示：

```
myMood=hopeful
```

如果在 set 命令后仅输入变量名的第一部分，则所有以该文本开头的变量将会被显示。因此，以下命令可能会输出比 myMood 变量的值更多的内容：

```
set myM
```

也许，真的也许，它会输出如下内容：

```
Mymar=A Genus of Fairyflies
myMood=hopeful 
```

虽然不太可能设置这样的变量，但如果它存在，并且如果这些是以 mym 开头的唯一两个变量，那么输出的结果将是这些变量的值。请注意，该命令会查找所有变量，忽略大小写。同时还要注意，变量值中包含了嵌入空格的示例。

在看到此技巧在完整变量名和部分变量名中都能正常工作之后，我们可以将其扩展到完全没有变量名的情况：

```
set
```

这个没有参数的命令会生成所有活动变量的列表，这些变量是在 bat 文件启动时加载的，以及来自 bat 文件本身的任何添加和修改。

### 持久设置变量

set 命令本质上是临时的。它在特定的批处理流中定义变量，直到变量被重新赋值或脚本终止，此时通过 set 命令设置的所有变量都会消失在记忆中。但有时我们希望一个变量能够供其他进程或计算机上的其他 bat 文件访问——甚至在原始 bat 文件终止后很久，甚至在计算机关机和重启后也能访问。我们需要的是一种极限版的 set 命令，或者叫做 *set Xtreme* 命令。名字起得很恰当，我们有了专为此目的设计的 setx 命令。（说实话，我不知道这个命令名字的来源，但这是我在聚会上讲的一个故事。在引言中你已经被提醒过不要邀请我这种人去参加派对。）

合乎逻辑的假设是 set 和 setx 命令的语法应该是相同的。你还能期待什么呢，除了变量、等号和一个值？这有点令人困惑，但事实并非如此。setx 命令不需要等号。相反，变量名和值之间由空格或多个空格分隔，如下所示：

```
setx myMood puzzled
```

在讨论 set 命令时，我们明确看到变量名和值确实可以包含空格。这显然会在设置变量的命令中造成困扰，因为空格是命令的分隔符。但将变量名和/或值用双引号括起来，可以轻松解决这个问题。这个命令创建了一个有两个词的变量名，并将一个包含两个词的值赋给它：

```
setx "my mood"  "cautiously optimistic"
```

要测试这个，首先在一个 bat 文件中执行上面的命令，然后在另一个 bat 文件中或直接在命令提示符下执行以下命令：

```
set my m
```

要查看效果，你必须在 setx 命令完成后启动第二个 bat 文件或打开命令提示符，因为当会话开始时，解释器会加载计算机现有的变量。

所有以我的 m 开头的变量（不区分大小写，包括嵌入的空格）都会被显示，包括变量 my mood。除非该变量被其他进程重新赋值，例如未来的 setx 命令，否则它会在计算机可操作时一直存在，直到被改变。

setx 命令是一个很棒的工具，可以帮助开发已编译的代码。当开发中的某个程序最终在生产环境中运行时，显然它会在不同的机器上运行，并且有自己的环境变量。在启用该程序时，一些 IDE 有很好的机制来模拟这些环境变量和文件连接设置；而其他 IDE 遗憾地没有。我见过一些不太优雅的解决方案来绕过这个缺陷，但一个好的解决方案是实际上在开发机器上设置所有需要的值，然后再启用程序。

对于用某些语言开发的每个程序，我会创建一个包含一系列 setx 命令的 bat 文件，每个变量都需要被持久化设置。执行 bat 文件后，我可以启用程序，它会在后续的生产环境中找到我预期的所有环境变量。如果我想启用另一个程序，我可以首先快速且轻松地运行与其关联的 bat 文件。如果在操作完成后，我希望恢复某些变量的先前状态，我也可以为此创建一个 bat 文件。（某些集成开发环境（IDE）在打开时只会存储一次环境中的所有变量。如果你的 IDE 是这种行为，确保在打开 IDE 之前运行 bat 文件。）

### 命令行帮助

本章将介绍的最后一个批处理命令是记录我已经讨论过的命令以及将来会介绍的所有其他命令的命令。help 命令接受另一个命令作为参数，并返回关于该命令的大量信息，从简要的功能描述和一般语法开始。

我将使用 set 命令进行演示，因为它在本章中起着核心作用。要调用 help 命令，请在命令提示符下输入它（在 Windows 开始菜单中输入 **CMD** 然后按回车）。接着，要获取更多关于 set 命令的详细信息，请输入以下内容：

```
**help set**
```

解释器生成了关于 set 命令的过多信息，无法在此完全显示，但以下是前几行内容：

```
Displays, sets, or removes cmd.exe environment variables.

SET [variable=[string]]

  variable  Specifies the environment-variable name.
  string    Specifies a series of characters to assign to the variable. 
```

命令的简要描述后紧接着是其一般语法，显然语法的开头是命令名本身。所有方括号中的文本（也称为硬括号）是可选的。方括号包围的文本，[variable=[string]]，表示命令可以使用或不使用括号内的文本。请记住，未带参数的 set 命令会返回所有活动变量的列表。嵌套的方括号进一步表明，string 也是可选的——也就是说，variable 可以设置为空值。

一些命令的帮助信息中提供了该命令的使用示例、附加说明和可用选项列表。*选项*是指为命令分配的设置或调整，用于开启或关闭某些附加功能。它们也叫做*开关*；事实上，帮助命令令人沮丧地将这两个术语交替使用。为了保持一致性，我将仅使用更常见的术语*选项*，但如果有人提到批处理命令开关，他们指的其实是选项。

选项通常由一个斜杠和一个字母组成，但你最终会遇到一些更复杂的选项。在命令提示符下滚动显示 set 命令的帮助信息，可以看到两个有趣且有用的选项。/A 选项允许命令执行算术运算（第六章）。/P 或 prompt 选项用于通过用户输入的数据设置变量（第十五章）。

在第一章中，你可能遇到了第一个批处理文件，其中包含了一个用于复制文件的命令。我不会在第七章中详细讲解 xcopy 命令，但在前一章中，它使用了三个选项（/F、/S 和 /Y）。这些选项具体的作用暂时不重要——但它们能够开启或关闭某些功能，并且这些选项在帮助命令中有文档说明。

一些命令有许多选项，其他命令则只有少数选项，甚至有些命令没有选项。在介绍命令时，我会详细讲解我认为重要且实用的选项，但你还需要使用帮助命令来查找更完整的列表。然而，一些未记录的选项无法通过帮助命令找到，要发现这些隐藏的宝藏，请访问 *[`<wbr>ss64<wbr>.com<wbr>/nt<wbr>/`](https://ss64.com/nt/)* 或其他相关资源。

我建议在第一次使用某个特定命令时，或者作为可用选项的提示时使用帮助命令。尝试将其用于本章中提到的任何其他命令，甚至是帮助命令本身。是的，在命令提示符下输入此命令

```
**help help**
```

显示有关帮助命令的文档。

> 注意

*在任何批处理命令后跟上* /? *将检索相同的信息。也就是说，输入* set /? *的效果与* help set*相同。*

### 总结

批处理编程可能很复杂，即使是看似简单的设置变量也可能有一些细微差别。在本章中，我详细介绍了 set 命令及其特殊之处，并将其与 setx 命令进行了比较，后者设置的变量是持久化的。你还学习了如何解析变量并将其显示在控制台上。现在你可以向批处理文件中添加注释，使用命令分隔符来实现多种功能，最重要的是，能够快速访问任何命令的文档。

在下一章，我们将进一步探讨变量，特别是变量的作用域。我们将研究如何定义变量在何时何地拥有特定的值，以及如何启用强大的延迟扩展功能。作为预告，我将告诉你一个秘密：一个变量的值可以是另一个变量的名称，而这个变量本身有自己的值。
