

## 第七章：7 操作文件



![](img/chapter.jpg)

如果你问一个对 Batch 只有间接了解的程序员，它的主要用途是什么，他们的回答很可能会提到移动文件。Batch 能做的远不止这些，但毫无疑问，它的主要用途之一就是文件操作。在本章中，我们将探索可用的不同命令和技巧。你还将学习如何创建空文件，以及合并、移动、重命名和删除文件的方法。我将介绍文件掩码和通配符，允许你对许多同名文件而不仅仅是一个文件执行你即将学习的命令。

### 复制文件的命令

Batch 有三个复制文件的命令：copy、xcopy 和 robocopy。在这一节中，我将对它们进行比较，并提供何时使用每个命令的建议，因为它们各自有不同的应用场景。你是在复制许多小文件还是几个大文件？网络稳定吗？速度是一个考虑因素吗？你希望返回码是简单的还是更细致的？日志记录对你有多重要？你需要回答许多问题，才能决定在特定的复制任务中使用最合适的命令和选项。

#### copy

copy 命令提供了一种快速简便的方法来创建空文件：

```
copy nul C:\Target\EmptyFile.dat
```

在 Batch 中，nul 这个词表示一个始终为空的文件（显然，有人觉得需要缩写 *null*）。在这里，我们通过复制 nul 文件来创建一个空文件，文件路径和名称由我们选择（在第十二章中，我将演示如何通过将不需要的输出发送到这个文件来处理它，让它消失）。有趣的事实是：Windows 不允许你在任何文件夹中创建名为 nul 的文件，无论扩展名是什么，或者没有扩展名，甚至手动创建也不行。你可以试试。

我使用复制命令的唯一其他场景是合并两个或更多相对较小的文件。/B 选项执行 *二进制* 文件复制，这意味着每一个字节，包括回车符和换行符等特殊字符，都会被原样复制，从而实现文件的真实连接。源文件通过加号分隔，后面跟着合并后的文件：

```
copy /B C:\Source\Header.txt + C:\Source\Details.txt C:\Target\MergedFile.txt
```

我们在这里将两个文件连接起来，但我们可以通过更多的加号分隔符合并更多文件。

你可能已经注意到，我只提到了复制命令的两种用途，而且都不涉及复制实际的文件。你可以使用该命令复制文件，但我从不使用它，因为它过于原始，选项也很少。这些不足很快在 Batch 首次发布后就显现出来，随后它很大程度上被更有用且可配置的 xcopy 命令取代。

#### xcopy

xcopy 命令的基本语法有两个参数——源文件和目标路径：

```
xcopy C:\Source\File2Copy.txt C:\Target\
```

xcopy 命令的一个巨大优势是，如果目标目录尚不存在，它会自动创建该目录。相比之下，copy 命令找不到路径就会失败。

在前面的示例中只给出了目标路径，这意味着复制的文件将与源文件同名。但我们可以通过简单地提供文件名来在 xcopy 命令中重命名目标文件：

```
xcopy C:\Source\File2Copy.txt C:\Target\RenamedFile.dat
```

这个命令有时会失败，因为解释器无法确定目标是目录还是文件（在第十二章中，我会讨论如何让这始终正常工作）。

xcopy 命令有两个选项，我几乎每次调用命令时都会使用。/Y 选项抑制确认覆盖目标文件的提示。虽然在通过命令提示符或交互式运行时，询问是否确认覆盖可能有意义，但在大多数其他情况下，它只是停止处理并造成卡顿，因此最好关闭它。

另一个我离不开的选项是 /F。它显示每个被复制文件的完整源路径和目标路径及文件名，这在使用通配符复制多个文件时，能留下有用的审计记录。如果没有 /F 选项，只会显示源文件或文件的路径，并给出文件总数。处于另一极端，如果你对这些信息不感兴趣，可以使用 /Q 选项（即*安静*模式）完全关闭显示。

使用这两个不可或缺的选项，以下命令会抑制提示并提供详细的日志记录：

```
xcopy C:\Source\File2Copy.txt C:\Target\ /Y /F
```

这个命令有太多可用选项，无法在此逐一介绍；请使用 help 命令查看完整列表。不过，作为一小部分示例，/U 选项仅复制目标中已经存在的文件；/S 选项复制文件夹及子文件夹；/J 选项使用无缓冲 I/O，适用于非常大的文件。

尽管 copy 命令已被弃用，xcopy 命令取而代之，微软实际上也认为 xcopy 本身已被弃用，取而代之的是更新的 robocopy 命令。尽管如此，xcopy 命令的广泛使用意味着它仍将在可预见的操作系统中可用，正如我稍后会详细说明的那样，在许多情况下它仍然是更好的选择。事实上，我在第一章中使用的就是这个命令，也许是你第一个 bat 文件的一部分。

#### robocopy

删除命令名中的最后一个字符，能揭示一部老科幻电影的标题，但 robocopy 实际上代表的是*强大复制*，这并非夸张。尽管 xcopy 拥有许多有用的选项，robocopy 提供了令人印象深刻的日志记录和令人目不暇接的选项。

尽管 robocopy 命令比 xcopy 强大得多，但它同样非常易于使用。参数略有不同：首先提供源目录（不带文件名），然后是目标目录，最后是要复制的文件、文件或文件掩码：

```
robocopy C:\Source\ C:\Target\ File2Copy.txt
```

这个示例在功能上与前一节中的 xcopy 命令等效，并在此重现：

```
xcopy C:\Source\File2Copy.txt C:\Target\
```

由于 robocopy 命令的前两个参数被认为是路径，因此你可以省略结尾的斜杠，这相比 xcopy 是一个很大的优势，因为在 xcopy 中，漏掉斜杠会把目标目录误当作文件名。

##### robocopy 日志记录

要了解 robocopy 的强大功能，不妨看看它的日志记录能力。如果你是 xcopy 的爱好者，最好先坐下再继续往下看。Verbose（详细）是唯一能形容它的词，甚至连这个词也不够准确。除了复制的文件列表，日志还包括一个漂亮的头部、开始和结束时间、文件大小、复制速度统计、源路径和目标路径、复制和跳过的文件和目录总数、任何失败的复制、使用的命令行选项列表等等。它的格式也很不错，有很多空白区域，便于阅读。

前面展示的简单 robocopy 命令用于复制一个文件，它会生成以下日志：

```
-----------------------------------------------------------------------------
   ROBOCOPY      ::      Robust File Copy for Windows
-----------------------------------------------------------------------------

  Started : Tuesday, January 30, 2007 12:18:44 PM
   Source : C:\Source\
     Dest : C:\Target\

    Files : File2Copy.txt

  Options : /DCOPY:DA /COPY:DAT /R:1000000 /W:30 

-----------------------------------------------------------------------------

     New Dir          1 C:\Source\
       New File         83146 File2Copy.txt
100% 

-----------------------------------------------------------------------------
 Total    Copied   Skipped  Mismatch    FAILED    Extras
    Dirs :         1         1         0         0         0         0
   Files :         1         1         0         0         0         0
   Bytes :    81.1 k    81.1 k         0         0         0         0
   Times :   0:00:00   0:00:00                       0:00:00   0:00:00

   Speed :            20786500 Bytes/sec.
   Speed :            1189.413 MegaBytes/min.
   Ended : Tuesday, January 30, 2007 12:18:44 PM 
```

一些结果不言自明，比如 Total（总数）、Copied（已复制）和 FAILED（失败），但其他结果则不太明显。如果要复制的文件已经存在于目标路径中并且完全相同，robocopy 会聪明地避免浪费时间覆盖它，而是将其视为合理的 Skipped（跳过）。当复制被阻止，因为源文件和目标文件夹（或源文件夹和目标文件）同名时，会发生 Mismatch（不匹配）。Extras（额外项）是指不在源目录中，但已经存在于目标位置的文件和目录。显然，这些文件并没有被复制，但解释器觉得有必要记录它们的存在。对于仅复制一个小文件的任务来说，日志记录可能显得有些过多。一个更有趣的任务可能会持续几页，但对于那个更有趣的任务，日志记录可能是不可或缺的。

默认情况下，所有这些信息都会写入控制台（在第十二章中，我会讨论如何将任何命令甚至整个批处理文件的日志重定向到日志文件）。但 robocopy 命令的独特之处在于，你可以轻松地通过选项创建日志文件。日志文件的路径和名称紧随其后，通过冒号与 /LOG 选项分隔。

这可能是你第一次看到选项后有多个字符的情况，而且这个命令还有更多类似的选项。将加号添加到选项中，如/LOG+，会导致信息附加到日志文件中（如果文件已存在）。为了演示，考虑以下内容：

```
set roboLog=C:\Batch\Robocopy.log
robocopy C:\Source\ C:\Target\ File2Copy.txt /LOG:%roboLog% /NP
robocopy C:\Source\ C:\Target\ AnotherFile2Copy.txt /LOG+:%roboLog% /NP 
```

在定义 roboLog 为日志文件路径和名称后，它会在两个 robocopy 命令中使用。第一个命令创建日志文件并写入复制信息，如果文件已存在，则会覆盖；第二个 robocopy 命令则将复制结果追加到该日志文件中。

最后两个命令都使用了/NP 选项，代表*无进度*，因为在创建日志文件时我认为它是必需的。如果信息被写入控制台，复制的进度会实时显示为百分比，并且每秒更新多次。较大的文件可能会显示几十个进度值，直到最终显示 100%。这种状态在控制台上查看时非常好，但在日志文件中，每次更新都会成为一个新记录。即使是几个大文件，也能让日志文件变得一团糟。即使是小文件，也可能导致日志文件中出现一些不需要的额外记录。/NP 选项非常好地解决了这个问题，它只会在每次复制完成后更新日志。

##### 有用的 robocopy 选项

robocopy 有许多选项，所以我将快速介绍其中最有用的几个。但首先，给出一个命令作为选项的基础：

```
robocopy C:\Source\ C:\Target\
```

这个简单的命令（没有文件的第三个参数）会将源目录中的所有文件复制到目标目录。

这个命令会自动重试失败的复制操作，功能非常棒，但默认的重试次数高达一百万次，每次之间间隔 30 秒。这可能会导致在进行有致命错误的复制尝试时，程序挂起几个月。/R 和/W 选项分别覆盖了默认的*重试*次数和*等待*时间（秒）：

```
robocopy C:\Source\ C:\Target\ /R:20 /W:5
```

使用这些选项时，解释器会在失败的复制操作中最多重试 20 次，每次尝试之间间隔 5 秒，之后才会中止。选择适合你的硬件和需求的值，但默认值不能继续使用。

如果添加了/S 选项，它还会复制所有的*子目录*，不过会排除空子目录：

```
robocopy C:\Source\ C:\Target\ /S
```

/E 选项会复制所有的子目录，包括*空*目录和非空目录：

```
robocopy C:\Source\ C:\Target\ /E
```

你可以精细化此选项，只复制源目录（*C:\Source\*）及其直接子目录中的文件，但不包括更低层级的子目录。/LEV 选项将子目录的*层级*（包括根目录）定义为 2，以此来处理：

```
robocopy C:\Source\ C:\Target\ /E /LEV:2
```

无缓冲 I/O 对于非常大的文件来说效率更高，而且通过/ J 选项调用，奇怪的是，它的实现方式与 xcopy 命令中的/U 选项有关（/U 已经被占用了）。/MIN 和/MAX 选项设置了文件复制时的*最小*和*最大*字节限制。以下两个命令会复制文件夹中的所有文件，只对至少 1GB 的文件使用无缓冲 I/O：

```
robocopy C:\Source\ C:\Target\ /MIN:1000000000 /J
robocopy C:\Source\ C:\Target\ /MAX:999999999 /MT 
```

我偷偷将/MT 选项（代表*多线程*）加到了小文件的命令中。默认情况下，robocopy 命令是串行复制文件的，但这个选项会并行复制八个文件。你甚至可以定义线程数；/MT:128 是最大值，但根据我的经验，这并不会比默认的八个线程快太多。我们可以讨论阈值，但对于大文件的无缓冲 I/O 和小文件的多线程操作，都会优化任何复制过程。

/MINAGE 和 /MAXAGE 选项（*最小*和*最大年龄*）定义了基于最后修改日期的复制规则。你可以单独使用它们，也可以结合使用以创建日期范围。以下命令只复制自 Microsoft 在 2000 年 9 月 14 日发布 Windows Me 以来修改过的文件，并排除过去七天内发生变化的文件：

```
robocopy C:\Source\ C:\Target /MAXAGE:20000914 /MINAGE:7
```

你可以将两个选项的值定义为日期（格式为 CCYYMMDD）或天数。解释器足够聪明，可以识别八字节数字表示的是日期，而不是超过 50,000 年的时间跨度。批处理可能不是一种新语言，但它既没有被丹尼索瓦人使用，也没有被我们的猎人-采集者祖先使用。任何小于 1,900 的数字都会被解释器认为是天数。

/PURGE 选项会删除目标位置的额外文件和目录，显然你应该谨慎使用，但它是创建备份时非常实用的工具。如果你在某一天备份了一个文件夹，第二天更改了源目录中某个文件的名称，然后再次备份该文件夹，除非使用 /PURGE 选项，否则备份中会多出一个不必要的文件。更好的做法是使用 /MIR 选项，它会*镜像*整个目录树。/MIR 选项基本上做的事情和 /PURGE 选项相同，但它还包括子目录。

/XF 选项会将一个或多个*文件*从复制中*排除*，而 /XD 选项则类似，*排除*一个或多个*目录*。/L 选项不会复制任何内容；它会生成一个*列表*，列出如果没有使用该选项，所有本应复制的内容。

你还可以使用 robocopy 命令来移动文件（参见第 80 页的“移动文件”）。像往常一样，使用帮助命令可以获得完整的可用选项列表。

##### robocopy 返回代码

xcopy 命令与大多数批处理命令类似，成功执行时返回错误级别 0，而失败时返回一个非 0 的数字。相比之下，robocopy 命令是独特的——如果不理解这一点，可能会感到非常困惑。返回代码 0 表示没有复制任何内容，但如果至少有一个文件成功复制，解释器会返回一个介于 1 到 15 之间的奇数，但即便是这些代码中，也并非都是*良好*的返回代码。以下是 robocopy 命令生成的六个基本返回代码：

0    没有错误，但没有文件被复制；换句话说，所有文件都被跳过了。

1    一个或多个文件复制成功。

2    发现了一个或多个额外的文件或目录，但没有任何文件被复制。

4    发现了一个或多个不匹配项；没有文件被复制。

8    某些文件或目录无法复制。

16    没有文件被复制；出现了严重错误。

哦，*二的幂*的优雅；一定是某个数学家想出了这些返回代码。难道用 0 到 5 这几个代码不更简单吗？不，中间的四个代码并不互相排斥；也就是说，它们中的多个（甚至全部四个）可以同时为真。解释器会将所有为真的代码加起来，并返回其总和作为错误级别。

举个例子，设想一个 robocopy 命令，将源文件夹中的所有文件复制到目标文件夹，其中一些文件复制成功，但目标文件夹也有额外的副本。返回代码 1 和 2 都为真，因此返回 3（它们的总和）作为错误级别。也可能在复制文件时，找到额外的文件，并且发生不匹配，另一个文件因为被某个进程或人占用而未能复制。做个数学运算（1 + 2 + 4 + 8），解释器返回 15。

这些返回代码为精明的编码员提供了细化错误处理的机会。返回代码 1 和 3 显然是好的，我通常认为 3 或更少也是好的。任何 4 到 7 的返回代码涉及不匹配，任何高于这个范围的返回代码至少包含一个显式的失败。根据具体情况，不匹配可能是完全可以接受的，因此只有返回代码为 8 或更大时才被视为不良。

为了明确验证至少有一个文件被复制，我们只需要查找一个奇数返回代码，这可以通过 第六章中的模运算函数来实现。

##### xcopy 与 robocopy

尽管“更好”这一词具有主观性质，但大多数批处理编码员一致认为 robocopy 比 xcopy 更*好*，因为它提供了丰富的选项、多线程、自动重试、强大的日志功能，以及许多后台操作，使得它更高效。但有一个主要的警告。当尝试复制单个文件时，xcopy 的错误处理显然*更好*。这两个命令都试图复制一个不存在的文件：

```
xcopy C:\Source\NonExistentFile.txt C:\Target\
robocopy C:\Source\ C:\Target\ NonExistentFile.txt 
```

xcopy 命令报告没有复制任何文件，并返回错误级别 4，而 robocopy 命令则简单地返回 0，表示没有错误也没有文件被复制。我认为这应该算作一个错误。当尝试复制多个文件时，如果没有源文件，两个命令都会返回 0——这很合理，但这两个命令都要求显式地复制一个文件，而 robocopy 的返回代码并未区分文件未找到和文件因为已存在于目标目录而被跳过。跳过未更新的文件是 robocopy 的一个很棒的功能，但它与文件未找到完全不同。强大的日志功能使这一点变得清晰，但返回代码并没有体现。

返回代码未能报告为何文件未被复制（未找到或被跳过）是一个缺陷，但我们可以克服它。使用 robocopy 命令复制特定文件时，你可以在 errorlevel 为 0 时对目标文件执行 if exist 检查。如果文件存在，说明文件已被正确跳过；如果不存在，则表示出现了错误。或者，像我一样，在这种情况下直接使用 xcopy。

还有一些情况下，robocopy 命令复杂的返回代码超过了实际需要。你可能不关心是否有额外或不匹配的文件，而只需要一个简单的结果：好与坏，零与非零。这样做并没有错，说实话，有时候我仍然因为习惯和使用方便而在新代码中使用它。

也就是说，robocopy 命令正如其名，具有更强的健壮性。它的配置更加灵活，速度更快，失败的可能性更小。如果需要复制非常大的文件、数量庞大的文件，或者因网络连接问题可能会失败的文件，robocopy 无疑是最佳选择，而它复杂的返回代码在某些时候也是一大优势。此外，xcopy 命令对路径和文件名的长度有 254 字节的限制。我从未接近过这个限制，但如果你真的碰到这个问题，robocopy 可以应对。

我对这个话题最明确的声明是，你永远不应该使用 copy 命令来复制文件，但可以将它保留在工具箱中，用于创建空文件或合并文件。

### 文件掩码和通配符

之前的所有 xcopy 示例都是复制单个文件，而 robocopy 示例要么复制单个文件，要么复制目录中的所有文件。但使用文件掩码和通配符后，你可以创建更具针对性的命令，只复制目录中的部分文件；实际上，它们每次执行时可能会复制不同数量的文件。*文件掩码*替代这两个命令中的文件名，并由一个或多个*通配符*字符组成，可能还包含一些硬编码文本或已解析的变量。然后，当命令执行时，它会复制所有符合文件掩码条件的文件。

文件掩码并非仅限于复制文件的命令。接下来本章中的移动、删除甚至重命名文件的命令同样接受文件掩码来代替文件名，这样你就可以一次性移动、删除或重命名多个文件。for 命令充分利用了文件掩码，我将在第二部分中展示如何使用。任何对文件执行操作的命令可能都支持通配符；如果不确定，可以尝试一下。

Batch 识别两个字符作为通配符：星号（*）和问号（?）。它们的行为截然不同，我将在使用 xcopy 命令时详细说明这两者，但首先让我们看一组需要复制的文件。一个精心组织的人可能会保持包含预算信息的电子表格，每个月一个文件。为了演示，*C:\Budget\* 文件夹中包含按年份和月份命名的电子表格。这里列出三个：

```
Budget.January2008.xlsx
Budget.February2008.xlsx
Budget.March2008.xlsx 
```

这些文件来自金融危机的年份和大萧条的开始，文件夹中还包含前后年份命名相似的文件以及其他类型的文件。

#### 星号通配符字符

星号是 Batch 中最常见的通配符字符，代表零个或多个字符。为了演示，以下的 xcopy 命令将复制所有 2008 年的文件，或者更具体地说，复制*C:\Budget\* 中满足 *Budget.*2008.xlsx 通配符的文件：

```
xcopy C:\Budget\Budget.*2008.xlsx C:\Target /F /Y
```

星号是通配符，意味着该命令复制所有以 Budget. 开头，2008.xlsx 结尾，且中间可以有任何内容，甚至没有内容的文件。如果文件夹中有一个名为 *Budget.2008.xlsx* 的文件，它也满足这个通配符并将被复制。

到年底时，应该有十二个这样的文件满足此通配符，一个对应每个月，这样命令就会复制所有 12 个文件。但该命令不会复制 2007 年或 2009 年的文件，也不会复制具有相同名称但扩展名不同的 Word 文档，例如 *Budget.June2008.docx*。如果你将其中一个文件的文件名中的第一个点删除，像 *BudgetAugust2008.xlsx*，解释器也不会复制它，因为它不满足通配符。

以下的细微变化——在通配符前插入字母 J——导致命令只复制三份文件，分别是 1 月、6 月和 7 月的文件：

```
xcopy C:\Budget\Budget.J*2008.xlsx C:\Target /F /Y
```

在通配符前再加一个字符，Budget.Ju*2008.xlsx 的通配符将排除 1 月的文件。

你不只限于在一个通配符中使用单一的星号。这里是一个例子，使用了两个星号，其中最后一个代表文件扩展名：

```
xcopy C:\Budget\Budget.*2008.* C:\Target /F /Y
```

前面提到的 Word 文档，*Budget.June2008.docx*，现在也被拖网捕获。

你现在知道星号（*）通配符可以表示任何长度的文本，包括不包含任何文本的情况，但有时你可能希望更具限制性。Batch（批处理）有一个鲜为人知且更少被理解的通配符字符，专门用于这个目的。

#### 问号通配符字符

虽然星号是零个或多个字符的通配符，但问号通常是精确匹配一个字符的通配符。（是的，斜体表示有个小陷阱。）为了只复制包含四个字符月份的文件，我将在原本是星号的位置使用四个问号：

```
xcopy C:\Budget\Budget.????2008.xlsx C:\Target /F /Y
```

该命令复制了 6 月和 7 月的文件，但没有复制 3 月、4 月、5 月以及所有其他月份的文件，这些月份的文件名通常较长。

这看起来很简单，但我之前承诺会告诉你一个注意事项。如果一个或多个问号通配符出现在文件掩码的末尾，或者这些问号紧接着一个点号，Batch 也会认为空字符是每个问号的有效替代值。

举个例子，我会稍微调整文件命名规则，在月份和年份之间插入一个句点，从而得到如下的文件名：

```
Budget.April.2008.xlsx
Budget.May.2008.xlsx
Budget.June.2008.xlsx
Budget.July.2008.xlsx 
```

接下来，我会将句点插入到文件掩码中的四个问号和年份之间，如下所示：

```
xcopy C:\Budget\Budget.????.2008.xlsx C:\Target /F /Y
```

即便是一些经验丰富的 Batch 程序员，可能也只会认为 6 月和 7 月的文件符合这个通配符条件。事实上，他们是对的，3 月、4 月以及所有其他月份（用超过四个字母表示的月份）的文件不会被复制，但 5 月的文件也会符合这个条件。前面三个通配符与“May”中的每个字母匹配，但第四个问号则与一个空字符或不存在的字符匹配。即使是命名奇怪的*Budget..2008.xlsx*也符合这个条件。

关键是，当*n*个问号通配符出现在文件掩码末尾或紧跟一个点号时，该掩码可以由零到*n*个字符满足。否则，*n*个问号通配符则要求正好*n*个字符。这是一个非常微妙的特性，理解它可能会节省你几个小时的麻烦。

为了更明确地说明这两个 Batch 通配符字符之间的区别，“shot”、“shoot”、“shut”、“shunt”、“shallot”甚至“sht”都符合 sh*t 文件掩码。对于这些单词（以及一个非单词），只有“shot”和“shut”符合 sh?t 文件掩码。

### 移动文件

移动文件类似于复制文件；唯一的区别是，复制文件后，它会存在于两个地方，而移动文件后，原始文件就不复存在了。移动命令可以轻松完成这一任务。它只需将源文件和目标文件作为参数，通常你会看到它带有/Y 选项，用于抑制覆盖目标文件时的确认提示：

```
move C:\Source\File2Move.txt C:\Target\ /Y
```

许多旧代码仍然包含移动命令，但它已被大部分弃用，转而使用 robocopy 命令。带有/MOV 选项的以下命令在功能上等同于之前的移动命令：

```
robocopy C:\Source\ C:\Target\ File2Move.txt /MOV
```

从这个 robocopy 命令中去掉文件名会移动源文件夹中的所有文件，但不会移动子文件夹中的任何文件。

添加/S 选项会导致移动所有子文件夹中的内容，即使目标子文件夹需要创建：

```
robocopy C:\Source\ C:\Target\ /MOV /S
```

该命令移动所有文件。文件不再位于源位置，但源文件夹结构保持不变。现在，这里有点奇怪，值得注意。我们一直在讨论 `/MOV` 选项，这显然是 *move*（移动）的缩写，但一个类似的选项 `/MOVE` 是指……我猜是 *带 E 的移动*。这两个选项有微妙的不同。带有 `/MOVE` 选项的 `robocopy` 命令真正地移动了文件和目录。

在选项中添加 E 会在将所有文件复制到目标位置后删除源目录结构：

```
robocopy C:\Source\ C:\Target\ /MOVE /S
```

我们有一个 `/MOV` 选项，它在复制子目录的同时移动文件，还有一个 `/MOVE` 选项，它移动文件和子目录。这个区别并不特别直观。

在考虑使用哪条命令来移动文件时，`robocopy` 命令是最有效的选择，原因和“xcopy 与 robocopy”中第 76 页提到的完全一样。但像 `xcopy` 命令一样，`move` 命令返回的代码更直观，仍然在批处理环境中有它的位置。

### 删除文件

当文件不再需要时，清理它们是很有道理的。`del` 命令可以轻松删除一个或多个文件。`/Q` 选项，代表 *quiet* 模式，可以防止解释器询问是否删除文件。该命令接受多个要删除的文件参数，既可以使用显式文件名，也可以使用文件掩码。以下命令删除名为 *Junk.txt* 的特定文件以及文件夹中所有扩展名为 *.OLD* 的文件：

```
del /Q C:\Source\Junk.txt C:\Source\*.OLD
```

使用 `/A` 选项根据文件的属性选择文件进行删除。例如，使用 `/AH` 选项仅删除隐藏文件。取反逻辑，`/A-H` 选项仅删除 *非* 隐藏文件。和往常一样，使用 `help` 命令查看完整的选项列表。

只需将目录作为参数传递给 `del` 命令，即可删除该文件夹中的所有文件，但目录本身仍然存在。要删除目录本身，你需要使用不同的命令，我会在第十三章中分享它。

### 重命名文件

`ren` 和 `rename` 命令是批处理命令的同义词；也就是说，它们是相同的命令。第一个参数是要重命名的文件，第二个参数是新的文件名：

```
ren C:\Batch\File2Rename.txt NewFileName.txt
```

如果目标文件已经存在，解释器会返回错误级别 1。如果有可能同名文件已经存在，我会在重命名之前悄悄删除它：

```
del C:\Batch\NewFileName.txt /Q
ren C:\Batch\File2Rename.txt NewFileName.txt 
```

即使对于此命令也支持通配符，但我只使用 `ren` 命令来显式指定文件名，主要是因为该命令不会在控制台上写出重命名文件的列表。（如果我要重命名多个文件，我会使用 `dir` 命令作为 `for` 命令的输入，然后一个一个地重命名。我们将在第十三章和第十七章进一步探讨这些命令。）

ren 命令并不复杂，但我见过一个常见的陷阱太多次了。虽然在两个参数中都使用路径很简单，但如果稍微思考一下，解释器已经从第一个参数中知道了路径。文件并没有被移动或复制到其他地方；根据命令的性质，它只是在当前位置被重命名。如果第一个参数没有路径，系统会默认当前目录（更多内容将在下一章介绍），但第二个参数仅是新文件名，不应该包含路径。

### 总结

本章可能会成为你在本书中最常引用的一章。如果没有创建、复制、移动、合并和删除文件的能力，Batch 就什么都不是，我在这里介绍了许多执行这些任务的命令。我讨论的一些命令非常简单，但我也覆盖了不止一个常见的陷阱，并提供了解决方案。你还学习了如何利用文件掩码和通配符在多个文件上同时执行这些命令。

复制文件听起来是一个简单的任务，但我详细介绍了可用的多种技术和需要考虑的因素。我希望我能向你展示 robocopy 命令的强大与实用，同时也让你认识到 xcopy 命令的简单性和实用性。

在下一章，我将描述如何执行在其他语言中编译的程序，这将涉及到更深入的讨论，探讨当你没有提供路径时，解释器是如何找到要执行的程序的。
