

## 第十五章：15 互动批处理



![](img/chapter.jpg)

如果你心中有任何疑虑，让我来告诉你，Batch 没有图形用户界面（GUI），但它有功能性的用户界面（UI）。在本章中，我将讨论如何在 bat 文件执行时获取用户输入的不同方式，比如从列表中选择一个选项或输入对某个问题的回答。我还会描述如何更改控制台的视觉显示或外观，包括清除屏幕、改变颜色以及更新标题。最后，我将把所有内容整合在一起，构建一个完全可操作的 Batch UI（BUI）准备执行。

### 用户界面、图形用户界面和批处理用户界面

用户界面本质上是用户与计算机之间进行沟通的一种方式，用户输入信息并获取反馈。每次你进行在线购买时，你都在使用图形用户界面，它是一种更复杂的用户界面，允许通过不仅仅是键盘的图形化用户输入。视频游戏是一个被美化的用户界面，每次你在智能手机上点击一个图标来打开应用时，你都在使用一个用户界面。《星际迷航：下一代》中的指挥官数据是一个安卓人，拥有一个极其先进的用户界面，能够通过五种感官与人类互动。

继续沿用科幻主题，Batch UI 更像是 1983 年的电影*战争游戏*。在 Batch UI 或 BUI（发音为 boo-ē）中，没有面板、下拉菜单、图标、菜单或单选按钮，更没有触摸屏或语音命令。请注意，如果你跟程序员提到这个词，可能会看到他们一脸茫然或扬起眉毛。我曾尝试过，但到目前为止未能将 BUI 纳入编程术语，但我仍然抱有希望，它会被接受并流行起来。

BUI 可能并不性感，但它能够向用户提问，用户可以通过输入一串文本或按一个键从列表中选择一个选项来回应。若是程序员用 Batch 来为大量用户构建一个复杂的用户界面，那简直是个施虐狂，更别说这位程序员很快就会失业了。但在许多情况下，bat 文件确实需要从用户那里获取一两个数据，尤其是当用户也是程序员时。我编写了很多 BUI，但每个 BUI 我都能数得出有多少人曾经使用过它。

BUI 可能有很多需求。你的 bat 文件可能需要将文件从或向服务器复制，并要求用户指定服务器。例如，你可能想创建一个报告，但能够根据用户的偏好从测试数据或生产数据中生成它。或者，你可能要求用户输入一个文件备份的日期范围。

### 从列表中选择一个选项

有两个命令允许用户输入数据到 BUI 中。一个要求用户从多个可能的选项中选择一个，另一个要求输入自由格式的响应。第一个是选择命令，顾名思义，它允许用户从两个或更多选项中做出*选择*。

为了开始，让我们问用户一个问题——你想要笑话、双关语还是谜语？——并允许他们输入 J、P 或 R，分别对应三种选择。以下选择命令正是这样做的：

```
choice /C:JPR /M:"Do you want a Joke, Pun, or Riddle"
```

/C 选项列出*选择*，/C:JPR，双引号括起来的文本与/M 选项相关联，是展示给用户的*消息*。这两个选项在没有冒号分隔符的情况下也能工作。也就是说，/C JPR 在功能上等同于/C:JPR，但我更喜欢使用冒号，因为它巧妙地将选项与其值或消息联系在一起，就像一块有价值的地毯能把房间融为一体。另外，注意问题末尾没有问号。解释器在向用户展示可选择项列表后会自动添加标点符号。

上一个命令在控制台上向用户显示以下内容：

```
Do you want a Joke, Pun, or Riddle [J,P,R]?
```

执行批处理文件时，命令会在选择命令处暂停，直到用户按下三个键中的一个（或退出命令窗口）。如果用户按下列表中没有的键，计算机会发出哔声并继续等待，但当用户选择其中一个选项时会发生什么呢？

到目前为止，errorlevel 仅仅是一个返回代码，通常为 0 表示命令成功执行，0 以外的数字表示执行失败。但在执行选择命令后，errorlevel 会被设置为用户的选择；更具体地说，它会被设置为与用户选择在列表中的位置相对应的整数值。更简单地说，如果选择是由/C:JPR 定义的，选择 J 会返回 1，P 返回 2，R 返回 3。

我觉得返回有效选择的变量名字中包含*error*一词有点误导，但在克服了语义问题以及这个保留字承担双重职责的事实后，检查变量以确定用户的选择并执行接下来的逻辑并不困难。

这是另一个选择命令的示例，似乎也缺少了一些内容（除了问号之外）：

```
choice /M:"Do you want to try again"
```

缺少了/C 选项，但这是一个是或否的问题，当省略此选项时，隐含的默认值是/C:YN，Y 返回 1，N 返回 2。

另外两个选项总是配合使用。/T 选项设置*超时*，即给定的秒数，在超时之前，命令会等待，解释器会选择/D 选项定义的*默认*选择。以下命令给用户 20 秒的时间，/T:20，在此之后会强制执行一个双关语，/D:P：

```
choice /C:JPR /M:"Do you want a Joke, Pun, or Riddle" /T:20 /D:P
```

有时列出选择项并不合适。例如，在询问用户给某项打分时，从 1 到 5 之间的评分，可能更倾向于通过文字来解释评分系统。假设你想向用户提出以下问题，要求其做出回应：

```
Rate your agreement to the statement on a scale of 1 to 5.
I love bat comedy.  1 (Agree) to 5 (Disagree) 
```

以下的 echo 命令和 choice 命令一起生成期望的文本，并等待响应：

```
echo Rate your agreement to the statement on a scale of 1 to 5.
choice /C:12345 /M:"I love bat comedy.  1 (Agree) to 5 (Disagree)" /N 
```

你将需要 /C:12345 选项，以便解释器能够知道所有可能的选择，但你不希望显示 [1,2,3,4,5]，因为它会与前一行的指示产生冲突。/N 选项（*无选择键*）会抑制选择的显示，只向用户显示期望的消息。问号也会随着 /N 选项被抑制，但如果消息是以问题的形式提问，你可以将问号包含在消息字符串中。

如果一个 bat 文件要在选定的服务器上执行任务，可以使用一系列的 echo 命令列出任意数量的服务器及其相关的键盘键，作为选择命令的前奏。对于预定义的服务器列表，这样做效果很好，但如果列表特别长或在编码时无法预知，可以改为要求用户输入服务器名称，作为下一个命令的输入。

### 自由格式的用户输入

另一个允许用户输入数据的命令是 set 命令（见第二章），当它与 /P 或 *提示字符串* 选项一起使用时。与没有选项的 set 命令类似，set /P 命令将一个值赋给变量，这个值可以是任何合理长度的字符串，甚至为空。不同之处在于等号后的文本不是赋给变量的值，而是显示给用户的提示字符串。用户输入的内容将在按下 ENTER 键后赋值给变量。

为了演示，等号后的问题将显示在控制台上：

```
set /P yourAns=How are bats like false teeth?  &
```

执行会被暂停，直到用户响应，此时解释器会将该响应赋值给 yourAns 变量。

如果忘记使用 /P 选项，变量将直接赋值为等号后面的文本，而不会提示用户。注意三个重要的细节。首先，这个命令没有在消息字符串后附加问号，所以我加上了。其次，我在问号后添加了一些空格，以便将响应的开始与问题分开。最后，我在行末加了一个 & 符号，让这些空格一目了然。这些细微的调整会让你的用户和任何阅读你代码的人都感到满意。

每个笑话都需要一个结尾，而这个结尾会在用户有机会思考问题并输入猜测后，出现在以下代码的第二行：

```
set /P yourAns=How are bats like false teeth?  &
echo ** They both come out at night.
echo ** You said: "%yourAns%" 
```

第三行和最后一行显示用户的答案，答案被双引号括起来。这是一个笑话。我并没有说这是一个好笑话。

### 修改界面外观和感觉

Batch 提供了三个更多的命令来改变控制台的外观和感觉，其中有一些命令还有其他用途：

**更新标题**

当一个 bat 文件执行时，会打开一个命令窗口，其顶部的白色条上的标题很可能是 *C:\WINDOWS\system32\cmd.exe*，即执行 bat 文件的程序。title 命令将这个标题重置为更具辨识度且不那么通用的标题。以下命令会将标题更改为紧随其后的文本：

```
title Batch Improv Theater 
```

参数列表中的嵌入空格不是问题，如果你将文本括在双引号中，它们也将成为标题的一部分。

使用这个命令不仅限于交互式 bat 文件，在其值显而易见的情况下也可以使用。任何在其他 bat 文件可能也在运行的机器上运行的 bat 文件，都可以通过标题得到增强。如果其中一个 bat 文件挂起或陷入死循环，应该终止哪个呢？如果它们没有标题，它们可能看起来一模一样，导致你无法知道是哪一个。title 命令解决了这个问题。实际上，我将在第二十六章中使用这个命令，给一个可能会挂起的进程添加标题，以便另一个 bat 文件能找到它并终止它，如果它确实挂起的话。你甚至可以在运行期间多次重置标题，可能显示当前状态或正在执行的步骤。

**清除屏幕**

cls 命令是 *clear screen* 的简写。当这个不带选项的命令执行时，屏幕或控制台会被清除，显示一个空白（目前是黑色）的画布。为了减少干扰，你可以在向用户提问之前执行这个命令。

**更改颜色**

打开或执行 bat 文件时，会弹出一个带有白色文本和黑色背景的命令窗口，这与人类自古埃及纸莎草和墨水出现以来的阅读方式完全相反。想象一下你现在正在阅读的文本，如果它是白色的，背景是漆黑的纸张。现在看起来似乎有些过时，但在 Batch 早期，它一定是前卫的。

color 命令提供了 16 种不同的颜色，供用户选择用于前景文本和背景，总共有 240 种组合，尽管某些组合几乎不可读，甚至会让眼睛感到不适。

进入帮助菜单，输入 color /? 来查看完整的颜色列表，颜色由十六进制数字 0 到 F 表示，但流行的颜色包括黑色（0）、蓝色（1）、红色（4）和白色（7）。color 命令接受一个两字符的颜色属性作为参数，其中第一个字符表示背景色，第二个字符表示前景色或文本色。顺便提一下，解释器足够智能，能够拒绝将相同颜色同时赋给背景和前景的命令。

黑色背景和白色文本是默认的 Batch 颜色方案，你可以通过以下命令显式调用它：color 07。将属性反转为 70 会创建一个白色背景和黑色文本的组合，但黑色文本配上明亮的白色（F）背景更加吸引人：

```
color F0 
```

我个人偏好的是在蓝色背景上使用亮白色文字，颜色 1F，但撇开美学不谈，这个命令最主要的用途是标记问题。你可能每天运行某些 bat 文件以执行一些日常或重复的任务。你可能每天早晨在登录时运行这样的 bat 文件，然后忽略它，但在文件无法复制或进程中止的罕见情况下，颜色命令提供了一种很好的方式，通过"红旗"（字面意义上）来警告用户。如果使用默认的颜色设置，写完错误信息后，这行命令会立即将屏幕从黑色（默认）变为红色（4），稍微加亮白色文字（F），并保持窗口打开：

```
@color 4F & pause 
```

即使用户已经转向其他任务并且命令窗口被置于一旁，这应该能够吸引他们的注意。如果没有，你也可以使用 cls 命令在写出错误信息之前清除屏幕。为了增强对比度，当过程成功完成时，你可以将背景设置为绿色，使用颜色 2F。

如果 stdout 已被重定向到追踪文件或 nul 文件，那么 cls 和 color 命令将不起作用。如果只需要清除屏幕一次，在重定向 stdout 到追踪文件之前执行 cls 命令；否则，echo off 是唯一能够关闭控制台输出的现实选择。同样，你可以在重定向前或后执行 color 命令。提前执行，它将为整个重定向过程设置颜色。你也可以在执行结束后返回重定向时，将屏幕变为红色，以表示中止。title 命令是其中最受欢迎的，因为它在 bat 文件中的任何位置都能正常工作，无论是否有重定向。

### 完全功能的批处理用户界面

让我们将这一切组合成一个完全功能的 bat 文件，可以反复互动地分享笑话、双关语或谜语。接下来的两个代码片段包含了完整的 bat 文件。这是*BatchImprov.bat*的第一部分：

```
❶ @setlocal EnableExtensions EnableDelayedExpansion
 @echo off
 color 1F
 title Batch Improv Theater

❷ :Again
 cls
 > con echo.
❸ > con choice /C:JPR /M:"Do you want a Joke, Pun, or Riddle"
 > con echo.
❹ if %errorlevel% equ 1 (
    call :Joke
 ) else if %errorlevel% equ 2 (
    call :Pun
 ) else if %errorlevel% equ 3 (
    call :Riddle
 )
 > con echo.
❺ > con choice /M:"Do you want to try again"
 if %errorlevel% equ 1  goto :Again
 goto :eof 
```

在 setlocal 命令❶（我常用的开头命令）之后，echo off 命令抑制 stdout，使得只有我生成的输出能显示在控制台上。注意，由于前面的 at 符号（@），这两个命令都不会写入控制台。然后，color 命令将背景设置为蓝色，文本设置为亮白色，目的是为了更好的可读性。接下来，title 命令定义命令窗口的标题。在:Again 标签❷之后，cls 命令清除屏幕，完成设置。

三个 echo.命令被策略性地放置在代码中，用于显示空白行，提升可读性。与之前相同的 choice 命令❸询问用户他们喜欢哪种幽默类型。由于有三种选择，if 命令配合 else if 结构根据 errorlevel❹的值判断用户的选择。根据用户的回答，分别调用笑话、双关语或谜语的不同 call 命令，分别对应 1、2 或 3 的值。

第二个选择命令❺询问用户是否希望继续欣赏这些幽默，默认选择为 Y 和 N。如果用户选择 Y，解释器返回 1 并且我们回到:Again 标签❷，在这里清屏并重新开始。选择 N 表示用户已经看够了，我们退出 bat 文件。

在前面的代码列表中调用的三个例程在*BatchImprov.bat*的最后部分定义如下：

```
:Joke
 > con echo Please give an answer to the joke:
 > con set /P yourAns=How are bats like false teeth?  &
 > con echo ** They both come out at night.
 > con echo ** You said: "%yourAns%"
 goto :eof

:Pun
 > con echo We hope you find this punny:
 > con echo Crossing a vampire bat with a computer means love at first byte.
 goto :eof

:Riddle
 > con echo Please give an answer to the riddle:
 > con set /P yourAns=This type of bat is silly.  &
 > con echo ** A Dingbat.
 > con echo ** You said: "%yourAns%"
 goto :eof 
```

:Joke 和:Riddle 例程结构相似。set /P 命令在揭示笑点和用户答案之前要求输入。:Pun 例程则仅仅输出机智的双关语，不需要用户输入。

这个 bat 文件并没有捕捉 stdout 和 stderr，因为这么做会阻止在每次选择之间使用 cls 命令。如果在开发过程中这成为问题，你可以暂时注释掉 echo off 命令，但要准备好面对一个凌乱的控制台，影响你的测试。

在*BatchImprov.bat*中的每个 choice 和 set /P 命令与本章前面展示的有所不同。除了 echo 命令外，每个命令都使用重定向，通过> con 语法显式地将提示或消息写入控制台。如果 stdout 被重定向到跟踪文件，这个重定向就显得必要；否则，它会将无法回答的提示字符串写入跟踪文件。但由于 stdout 只是被抑制了，控制台的重定向在这种情况下是多余的。我在本章早些地方没有包含重定向，是因为我想专注于介绍新命令，但在实际使用中，最好总是显式地定义目标，如我在这里所做的，即使它不是必需的。

*BatchImprov.bat* bat 文件现在已经完全功能化。运行它，你可以回答问题并查看结果，直到你在"Do you want to try again?"提示下选择 N。毫无疑问，由于内容有限，你很快就会感到厌倦，但我们仍然有许多增强功能要讨论。在附录 A 中，你会找到一个更加动态的 bat 文件版本，能够读取包含笑话、双关语和谜语的文件；将它们存储到数组中；并在单次执行中随机访问这些数组以获得独特的内容。

### 总结

在本章中，我创建了迄今为止最重要的 bat 文件，目的是演示如何与用户进行交互式通信。你学习了如何提供一个选择列表供用户选择，以及如何将用户输入的自由格式响应（无论长度如何）存储到一个变量中。我还介绍了用于清屏、更新标题、改变背景和文本颜色的有用命令，包括这些命令的其他非交互式用途。

在下一章，即第一部分的最后一章，我将讨论代码块，这是一个在你深入学习过程中不可或缺的话题。代码块不仅仅是“代码块”那么简单。我将在接下来的几页中解释它是什么、为什么它很重要以及它如何有用。
