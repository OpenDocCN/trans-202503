<hgroup>

## 13 使用目录

</hgroup>

![](img/chapter.jpg)

Batch 是一个理想的工具，用于查询 Windows 目录。目录中有哪些文件或文件类型？是否有某些目录快满了？丢失的文件在哪里？你可以通过一些 Batch 代码回答这些问题，甚至更多。

在本章中，你将学习如何创建目录、删除目录，以及如何获取关于现有目录的大量信息。很快，你将能够快速生成一个报告，详细列出目录的内容，包括文件名、子目录以及任何子目录的所有内容。信息可以包括所有文件的大小、最后修改日期和属性，或者仅选择某些文件。最终，你将学习如何在 bat 文件中逐一处理这些文件和目录，但我将从分享如何获取这些有用的数据开始。我还将探讨如何轻松确定特定文件或文件掩码是否存在。

最后，你将学习如何将本地和网络目录映射到驱动器字母。几乎所有使用 Windows 计算机的人都可以通过将本章的一些小技巧编入一个简单的 bat 文件，从而减轻一些日常的繁琐工作，提高工作效率。

### 目录基础

*目录* 是计算机磁盘驱动器上的一个映射位置，可以容纳文件和其他目录或子目录。在 Windows 计算机上，目录由文件夹表示；事实上，*目录* 和 *文件夹* 这两个术语经常可以互换使用。在 Windows 资源管理器中，你可以通过几次鼠标点击在目录中创建和删除子目录及文件。使用 bat 文件，你也可以做到这一点，可能还更简单。

#### 创建目录

要创建一个目录，Batch 使用 md 和 mkdir 命令。这两个命令都代表 *创建目录*，实际上它们是相同的命令（批处理同义词）。

md 命令不接受任何选项，唯一的参数是要创建的目录：

```
md C:\Batch\MakeMe\
```

这个既有用又简单的命令可以接受多个目录进行创建，但当你为每个目录使用单个命令时，每个命令的返回代码清楚地告诉你哪些目录已成功创建，哪些没有。md 命令可以在有无斜杠的情况下工作，但我建议使用它，理由仅仅是它的存在让参数看起来像一个目录。

#### 删除目录

md 命令的对等命令是 rd 命令，用于 *删除目录*，它会删除一个目录及其中的任何文件。它还有一个批处理同义词 rmdir，并且只有两个选项，我总是使用这两个选项：/Q 启用 *安静* 模式，/S 删除任何 *子目录* 及其内容：

```
rd /Q /S C:\DeleteMe\
```

此命令还接受一个到多个目录作为参数，带或不带尾部斜杠。如果没有子目录，该命令可以使用或不使用 /S 选项，但如果有一个或多个子目录，则没有任何东西会在没有选项的情况下被删除。我还无法概念化删除一个目录而不删除其子目录意味着什么，因此我总是使用 /S 选项。

### 检索目录信息

批处理有两个命令用于检索关于目录及其包含文件的详细信息，其中一个比另一个更有用。我将从不可或缺的命令开始。

#### dir 命令

一个极其有用的工具是 dir 命令，它是 *directory* 的缩写。许多人会在命令提示符下使用它来在控制台上显示信息。当在批处理文件中单独使用时，该信息只是简单地输出到标准输出流(stdout)，通常意味着它被 stdout 中包含的所有其他内容所包含，因此并不是很有用。但是，通常您会以以下两种方式之一使用 dir。

首先，根据您在第十二章中学到的知识，您可以将命令的输出重定向到文件中供程序、人类或最终批处理文件读取。其次，更令人印象深刻的是，我将展示如何将 dir 命令输入到 for 命令中，跳过创建文件的步骤。这将在第二部分中讨论，但在您使用 dir 命令输入 for 命令之前，您需要了解 dir 命令本身的复杂性。

##### 没有选项

最简单的 dir 命令接受一个单一的参数：目录或文件夹。如果路径中没有嵌入空格，则双引号是可选的：

```
dir "C:\Important Stuff\"
```

为了演示目的，假设该目录包含一些重要个人文档的电子副本和几个其他重要材料的子目录。命令的结果可能是向 stdout 显示的列表 13-1 中显示的格式良好的报告。

```
 Volume in drive C is OS
 Volume Serial Number is 2E7D-DB30

 Directory of C:\Important Stuff

07/25/2020  05:19 PM    <DIR>          .
07/25/2020  05:19 PM    <DIR>          ..
10/05/2019  06:44 PM           280,643 Birth Certificate.jpg
04/01/2014  08:28 PM           120,542 Car Title.pdf
07/25/2020  05:18 PM            61,124 Passport.png
07/25/2020  05:20 PM    <DIR>          Retirement
07/25/2020  10:51 PM            64,760 SSI Card.png
07/18/2020  02:26 PM    <DIR>          Taxes
               4 File(s)         527,069 bytes
               4 Dir(s)  173,275,090,944 bytes free 
```

列表 13-1：无选项 dir 命令的示例输出

经过三行标题后，前两个 <DIR> 条目表示这是一个子目录，而不是与驱动器号相关联的根文件夹；这两行在 dir C:\ 命令中不会出现。更有趣的是，此文件夹中的所有文件都清晰显示，每个文件都带有其最后修改的日期和时间以及文件大小。子目录也清晰地显示出来。

注意，文件和子目录是交错显示的，默认按字母顺序排序，但正如您很快将看到的那样，该命令非常可定制，具有其选项，可以极大地控制输出。

您甚至可以在单个命令中列出多个目录：

```
dir "C:\Important Stuff\" C:\Batch\
```

此命令显示第一个文件夹下的所有文件和子目录，然后是另一个标题下的第二个文件夹的类似信息：C:\Batch 目录。

##### 一些有用的选项

dir 命令有很多有用的选项；其中有几个选项甚至还有自己的选项。我将讨论我经常使用的那些选项，但如常，完整的选项列表可以在帮助中找到，命令为 dir /?

/O 选项控制排序*顺序*，但它的工作方式与到目前为止你所见的大多数选项略有不同。附加字符定义了排序顺序。例如，/OG 将目录排在文件前面，而减号则反转排序顺序，/O-G 将文件排在目录前面。（令人痛苦的是，G 代表*先显示目录*。）/OEN 选项按*扩展名*和*名称*排序，而/O-E-N 则反转顺序。有些人可能觉得它*讨厌*，但是/ODS 按修改日期和时间排序，然后按文件大小排序：

```
dir "C:\Important Stuff\" /ODS
```

*属性*选项，/A，限制显示内容。/AH 选项仅列出隐藏文件，而/A-H 则从列表中省略隐藏文件；/AD 仅显示目录，/A-D 则不显示目录。实际上应该有一个仅针对文件的选项，但“无目录”选项能够完成任务，就像只有批处理能做到的一样。

默认情况下，文件大小以逗号分隔，方便阅读（对于人类来说），但如果我们想对这些数字进行任何算术操作，/ -C 选项会去掉逗号。编码者经常希望生成一个没有任何杂乱的简单文件名列表。/B 选项，即*裸*选项，能够很好地完成这个任务。

将这些选项组合在一起，以下命令跳过目录（/A-D），按大小从小到大排序（/OS），并仅显示文件名而不显示其路径（/B）：

```
dir "C:\Important Stuff\" /B /A-D /OS
```

这条没有任何选项的命令生成了列表 13-1 中的报告。使用这些选项后，结果简洁得多，仅仅是一个简单的文件名列表：

```
Passport.png
SSI Card.png
Car Title.pdf
Birth Certificate.jpg 
```

从列表 13-1 中可以看到，护照是最小的文件，出生证明是最大的，这表明尽管没有显示字节数，文件仍然按大小排序。简洁的输出可能看起来像是一种降级；确实，它对人类不如之前那么有信息量，但在第二部分中，这将是理想的数据，可以输入到一个 for 命令中逐个处理文件。

另一个有用的选项是/S，代表*子目录*。它本质上是先对一个目录运行 dir 命令，然后对其所有子目录再次运行此命令，返回一个格式良好的报告，每个子目录都有小标题。dir C:\ /S 命令提供了你计算机上每个文件夹的报告，但速度不快，结果可能比本书还长。当与/B 选项一起使用时，每个纯文件名都会加上其路径——这可能看起来像是一个矛盾，但在考虑到每个文件可能位于多个目录中时，就能理解了。

##### 默认选项集

如果你打算运行多个 dir 命令，而且每个命令的选项都相同，那么你不需要为每个命令都重复这些选项。相反，你可以将一个或多个选项加载到 dircmd 伪环境变量中。一旦设置，所有后续的 dir 命令将默认使用 dircmd 变量中的选项。

例如，以下代码执行两个 dir 命令，显示裸文件名 (/B)，不显示目录 (/A-D)，并按扩展名排序 (/OE)：

```
set dircmd=/B /A-D /OE
dir "C:\Important Stuff\"
dir "C:\Some Other Folder\" 
```

你可以为特定的 dir 命令覆盖 dircmd 变量中的一个或多个选项。假设接下来的 dir 命令遵循了前面的代码，其中 dircmd 已经设置：

```
dir "C:\Important Stuff\" /O-E
```

这仍然使用了仅适用于文件裸格式的选项，但排序顺序被反转了。

任何时候，你都可以通过将 dircmd 设置为空或完全不设置来关闭此功能。

#### where 命令

where 命令类似于 dir 命令；它搜索一个或多个目录来查找文件 *所在的位置*。如果你在读这段话时耸了耸肩，心想：“dir 命令不也能做这个，而且还能做更多吗？”那么我的回答肯定是肯定的。大多数你能用 where 做的事情，dir 也能做，只是做得更好。

然而，where 命令在执行某些任务时，比 dir 命令更高效。使用 /Q 选项时，where 命令返回一个退出代码，表示成功或失败，而不是返回找到的文件列表，这使得判断某个文件是否存在或是否有至少一个与文件掩码匹配的文件变得更加容易。（/Q 选项代表 *安静* 模式，像你见过的其他模式一样，但在此命令中，"安静"的含义稍有不同。对于其他命令，提示被抑制，但在这里，输出被抑制。）

以下命令会在文件夹 *C:\Batch\* 中查找至少一个以 FindMe 开头的文件，忽略大小写，并根据结果设置错误级别：

```
where /Q C:\Batch\:FindMe*
```

如果至少有一个文件符合掩码，返回代码将为 0；如果没有，则为 1；如果语法不正确，则为 2。

如果你仔细查看，会发现路径和文件掩码之间似乎有一个多余的冒号。dir 命令正确地将路径和文件名（或掩码）视为一个参数一起处理。而 where 命令将它们视为由冒号分隔的两个参数。这确实允许你用一个文件名或掩码输入多个由分号分隔的路径，但这并没有多少安慰。更令人困惑的是，当使用 /R 选项时——表示 *递归*，即它还会搜索子目录——冒号会被空格替代：

```
where /Q /R C:\Batch\ FindMe*
```

我勉强包括了这个命令。语法本身就是错误的，但它确实有一个值得使用的功能。可以将其视为带有返回代码的 if exist 命令。仅在执行这个特定任务时使用它，其他情况请继续使用 dir。

### 映射驱动器字母

批处理有两个非常有用的命令用于映射驱动器字母。一个是将本地路径映射到驱动器字母，另一个是将网络路径和共享映射到驱动器字母。如果您不是编程人员——首先，给您慢拍手，恭喜您已经阅读到第十三章——但更重要的是，如果这听起来像是只有程序员才能使用的东西，那其实并不是这样的。

映射路径是一个很好的工具，特别是如果您在一天中经常在计算机或网络的多个特定路径下工作。导航到这些路径可能会花费一些时间，特别是当这些路径深层嵌套时。另一个挑战，尤其是如果您在家工作，可能是您在连接到虚拟私人网络（VPN）之前无法看到网络路径。为了简化这一切，编写一个简短的批处理文件，每天早上运行，可能是在连接到网络后，这样您就可以轻松访问这些路径了。映射完成后，通过点击 Windows 资源管理器中的驱动器字母即可访问每个路径。

#### `subst` 命令

`subst` 命令将本地目录或您 Windows 计算机上的任何文件夹映射到驱动器字母。命令名称是 *substitute* 的缩写，因为使用它后，您可以用驱动器字母替代目录。（不，这不是用来做子字符串操作的。）以下命令将 *Z:\* 映射到所示路径，尽管如果路径不存在或驱动器字母已映射到另一个路径，则会失败：

```
subst Z: C:\ParentFolder\ChildFolder\GrandchildFolder\
```

执行此命令后，您将在 Windows 资源管理器中找到 *Z:\*，它作为 *C:\ParentFolder\ChildFolder\GrandchildFolder\* 路径的别名。现在，批处理文件可以仅通过调用驱动器字母来访问该路径中的任何内容。例如，在执行前述命令后，以下命令将在 *GrandchildFolder\* 目录中创建一个空文件，从而减少输入量：

```
copy nul Z:\EmptyFile.txt
```

`subst` 命令如果没有参数或选项，将显示所有当前通过之前的 `subst` 命令映射的文件夹。如果在之前的 `subst` 命令之前，已经有其他文件夹被映射，简洁命令将...

```
subst
```

可能会生成以下输出：

```
Y:\: => C:\Certain\Other\Folder\
Z:\: => C:\ParentFolder\ChildFolder\GrandchildFolder\ 
```

*Z:\* 的映射现在将在计算机上生效，甚至对于其他进程（无论是批处理还是人工操作），直到计算机注销或运行以下命令通过 /D 选项 *删除* 或断开该映射：

```
subst Z: /D
```

此命令仅映射本地目录；它不会映射其他计算机上的目录，但还有一个其他命令可以完成这个任务。

#### `net use` 命令

`net use` 命令将网络目录和共享映射为 `subst` 命令映射本地目录。以下命令将 *Y:\* 映射到远程服务器上的共享：

```
net use Y: \\RemoteServer\ShareName\
```

您现在可以使用驱动器字母 *Y:\* 来访问另一台计算机上的此路径，再次适用于批处理和人工操作，直到映射被删除或计算机关闭。与 `subst` 命令类似，`net use` 命令也有一个选项来断开或删除映射，但它更为冗长：

```
net use Y: /DELETE
```

/D 和/DELETE 选项的创始人至少在潜意识里是在尊重那句著名的名言：“愚蠢的一致性是狭隘心智的鬼怪。”拉尔夫·沃尔多·爱默生可能会不赞同，但为了致敬这种愚蠢的一致性，我像对待其他选项一样将这个冗长的选项大写化，因为这是我的惯例，但并非没有保留。我的这一惯例背后的主要动机是尽量减少大写字母的使用，但当一个通常是单个字符的选项变为六个字符时，它自然应该使用小写字母。做你觉得对的事情。

与 subst 命令类似，net use 命令如果没有附加参数，将会列出通过早期 net use 命令映射的所有目录和驱动器。

> 注意

*我需要先预防一些恶评。这实际上是 net 命令，其中 use 是它的第一个参数。该命令有十多个其他的第一个参数——例如，share 用来创建文件共享。但是由于 use 的广泛使用，程序员通常称其为 net use 命令。事实上，help net use /?会像任何其他命令一样提供相关信息。*

### 总结

在这一章中，你学习了如何创建和删除目录。我详细介绍了极为重要的 dir 命令，稍后你还会再见到它，还有 where 命令，你在本书中不会再见到它，因为之前提到的原因。现在，你可以获取关于目录及其所有内容的惊人信息，在第二部分中，我将演示如何遍历这些数据，以便你可以对每个文件或目录执行任务。你还学会了如何映射本地和网络目录，这对于任何人来说都是极其有用的技能，不仅仅是程序员，尤其是那些经常在 Windows 计算机上工作的用户。

转换话题，我接下来将深入探讨逃逸的概念。对某些人来说，这可能是本书中最让人困惑的标题；什么是逃逸，逃逸的是什麼，我们又在试图逃避什么？这些问题和更多的疑问将在下一章中解答。
